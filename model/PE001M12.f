C**** PE001M12 E001M12 SOMTQ PB395M12
C**** OPT(3)
C**** semi-random cloud overlap (+snow age updates+computed opt.d+diagn)
C**** to be used with R99E or later radiation  routines.  carbon/2
C**** Constant pressure at L=LS1 and above (SIGE(LS1)=0., PLE(LS1)=PTOP)
C**** Using 5 harmonics for horizontal ocean heat transport, thinner ice
C**** Routines included:  PRECIP, COSZ0, RADIA,
C****                     GROUND, DRYCNV, SDRAG
*****
C*    Sea ice has four thermal layers
C*    The lead fraction is ice thickness dependent
C*
      SUBROUTINE PRECIP
C****
C**** THIS SUBROUTINE USES THE PRECIPITATION TO CALCULATE THE GROUND
C**** WATER, GROUND ICE, SNOW COVER, AND RUNOFF
C****
C**** RUN1 IS NOT ACUMULATED IN ADAILY FOR DIAG6
C****
      USE CONSTANT, only : grav,rgas,kapa,sday,lhm,lhe,lhs,twopi,omega
     *     ,rhow,rhoi,shw,shi
      USE E001M12_COM
      USE GEOM
      USE CLD01_COM_E001, only : PREC,TPREC
      USE DAGCOM  !, only : aj,bj,cj,areg,aij,jreg
      USE OCEAN, only : ODATA,OA,Z1O,ACE1I,PREC_SI,PREC_OC 
      IMPLICIT NONE
      REAL*8 MSI1, MSI2

      REAL*8, PARAMETER :: Z1E=.1, Z2LI=2.9
      INTEGER I,J,L,K,IMAX,JR
      REAL*8 ACE2LI,HC1I,HC1DE,AENRGP,BENRGP,CENRGP,BEDIFS,CEDIFS,AEFO
     *     ,BERUN0,BERUN2,CERUN2,AERUN4,CERUN4,BDIFS,CDIFS,AIFO,BRUN0
     *     ,CRUN0,BRUN2,CRUN2,ARUN4,CRUN4,PLAND,PWATER,PLICE,PEARTH
     *     ,ROICE,POICE,POCEAN,DXYPJ,RUN0S,DIFSS,PRCP,TPRCP,EPRCP
     *     ,ENRGP,TGW,WTRO,RUN4,ERUN4,SNOW,WTRW0,ENRGW0,DIFS,EDIFS,WTRW
     *     ,ENRGW,TG1,TG2,HC1,DWATER,RUN0,ERUN2

C****
C**** FLAND     LAND COVERAGE (1)
C**** FLICE     LAND ICE COVERAGE (1)
C****
C**** ODATA  1  OCEAN TEMPERATURE (C)
C****        2  RATIO OF OCEAN ICE COVERAGE TO WATER COVERAGE (1)
C****        3  OCEAN ICE AMOUNT OF SECOND LAYER (KG/M**2)
C****
C**** GDATA  1  OCEAN ICE SNOW AMOUNT (KG/M**2)
C****        2  EARTH SNOW AMOUNT (KG/M**2)
C****        3  OCEAN ICE TEMPERATURE OF FIRST LAYER (C)
C****        4  EARTH TEMPERATURE OF FIRST LAYER (C)
C****        5  EARTH WATER OF FIRST LAYER (KG/M**2)
C****        6  EARTH ICE OF FIRST LAYER (KG/M**2)
C****        7  OCEAN ICE TEMPERATURE OF SECOND LAYER (C)
C****       12  LAND ICE SNOW AMOUNT (KG/M**2)
C****       13  LAND ICE TEMPERATURE OF FIRST LAYER (C)
C****       14  LAND ICE TEMPERATURE OF SECOND LAYER (C)
C****       15  OCEAN ICE TEMPERATURE OF THIRD LAYER (C)
C****       16  OCEAN ICE TEMPERATURE OF FOURTH LAYER (C)
C****
C*    WTRO   - Liquid ocean mass (kg/m^2)
C*
C*    ROICE  - Horizontal ratio of sea ice to ocean (1)
C*    MSI    - Mass of sea ice (kg/m^2)
C*    HSI    - Enthalpy minus latent heat of sea ice (J/m^2)
C*
C*    POCEAN - Ocean fraction (1)
C*
C*    PREC   - Precipitation from atmosphere (kg/m^2)
C*    EPRCP  - Energy of precipitation (J/m^2)
C*
C****
C**** OUTSIDE LOOP OVER J AND I, EXECUTED ONCE FOR EACH GRID POINT
C****
      ACE2LI=Z2LI*RHOI
      HC1I=ACE1I*SHI
      HC1DE=Z1E*1129950.
C*
C*
      DO 980 J=1,JM
      IMAX=IMAXJ(J)
         AENRGP=0.
         BENRGP=0.
         CENRGP=0.
         BEDIFS=0.
         CEDIFS=0.
         AEFO=0.
         BERUN0=0.
         BERUN2=0.
         CERUN2=0.
         AERUN4=0.
         CERUN4=0.
         BDIFS=0.
         CDIFS=0.
         AIFO=0.
         BRUN0=0.
         CRUN0=0.
         BRUN2=0.
         CRUN2=0.
         ARUN4=0.
         CRUN4=0.
      DO 960 I=1,IMAX
      IF (PREC(I,J).LE.0.) GO TO 960
C****
C**** DETERMINE SURFACE CONDITIONS
C****
      PLAND=FLAND(I,J)
      PWATER=1.-PLAND
      PLICE=FLICE(I,J)
      PEARTH=FEARTH(I,J)
      ROICE=ODATA(I,J,2)
      POICE=ROICE*PWATER
      POCEAN=PWATER-POICE
         JR=JREG(I,J)
         DXYPJ=DXYP(J)
         RUN0S=0.
         DIFSS=0.
C**** CALCULATE PRECIPITATION HEAT FLUX (FALLS AT 0 DEGREES CENTIGRADE)
      PRCP=PREC(I,J)
      TPRCP=TPREC(I,J)
      IF (TPRCP.LT.0.) GO TO 30
C     EPRCP=PRCP*TPRCP*SHW
      EPRCP=0.
      ENRGP=EPRCP
      GO TO 50
C     EPRCP=PRCP*TPRCP*SHI
   30 EPRCP=0.
      ENRGP=EPRCP-PRCP*LHM
         AIJ(I,J,IJ_SNWF)=AIJ(I,J,IJ_SNWF)+PRCP
   50 CONTINUE
      IF (PWATER.LE.0.) GO TO 400
C****
C**** OCEAN
C****
            OA(I,J,4)=OA(I,J,4)+ENRGP
         AENRGP=AENRGP+ENRGP*POCEAN
         AIJ(I,J,IJ_F0OC)=AIJ(I,J,IJ_F0OC)+ENRGP*POCEAN
      IF (KOCEAN .EQ. 1) THEN
      TGW=ODATA(I,J,1)
      WTRO=Z1O(I,J)*RHOW
      RUN4=PRCP
      ERUN4=RUN4*ODATA(I,J,1)*SHW
         AERUN4=AERUN4+ERUN4*POCEAN
         ARUN4=ARUN4+RUN4*POCEAN
      IF (ROICE.GT.0.) GO TO 110
      CALL PREC_OC(I,J,ENRGP,ERUN4)
      GO TO 400
      END IF 
C****
  100 IF (POICE.LE.0.) GO TO 400
C****
C**** OCEAN ICE
C****
  110 CONTINUE
      SNOW=GDATA(I,J,1)
      MSI2=ODATA(I,J,3)
      MSI1 = SNOW + ACE1I
C*
         CENRGP=CENRGP+ENRGP*POICE
         AIJ(I,J,IJ_F0OI)=AIJ(I,J,IJ_F0OI)+ENRGP*POICE
      IF (KOCEAN .EQ. 1) THEN
         CERUN4=CERUN4+ERUN4*POICE
         CRUN4=CRUN4+RUN4*POICE
      WTRW0=WTRO-ROICE*(MSI1+MSI2)
      ENRGW0=WTRW0*ODATA(I,J,1)*SHW
      DIFS=0.
      EDIFS=0.
      END IF

C*
C***  CALL SUBROUTINE FOR CALCULATION OF PRECIPITATION OVER SEA ICE
C* 
      CALL PREC_SI(I,J,PRCP,TPRCP,EPRCP,RUN0,DIFS,EDIFS,ERUN2)
C*
      IF (KOCEAN .NE. 1) THEN
C*
C***  ACCUMULATE DIAGNOSTICS
C*
         CEDIFS=CEDIFS+EDIFS*POICE
         CDIFS=CDIFS+DIFS*POICE
         DIFSS=DIFSS+DIFS*POICE
         CERUN2=CERUN2+ERUN2*POICE
         CRUN2=CRUN2+DIFS*POICE
C*
      ELSE
C*
         CEDIFS=CEDIFS+EDIFS*POICE
         CDIFS=CDIFS+DIFS*POICE
C*
C**** CALCULATE THE COMPOSITE WATER MASS AND WATER ENERGY
C*
      WTRW=WTRW0+ROICE*(RUN0-RUN4)
      ENRGW=ENRGW0-ROICE*ERUN4+(1.-ROICE)*ENRGP
      TGW=ENRGW/(WTRW*SHW) ! mixed layer temperature
      ODATA(I,J,1)=TGW
      END IF 
C*
         CRUN0=CRUN0+RUN0*POICE
         RUN0S=RUN0S+RUN0*POICE
C****
  400 IF (PLICE.LE.0.) GO TO 600
C****
C**** LAND ICE
C****
      SNOW=GDATA(I,J,12)
      TG1=GDATA(I,J,13)
      TG2=GDATA(I,J,14)
         BENRGP=BENRGP+ENRGP*PLICE
         AIJ(I,J,IJ_F0LI)=AIJ(I,J,IJ_F0LI)+ENRGP
      HC1=HC1I+SNOW*SHI
      RUN0=0.
      IF (TPRCP.LT.0.) GO TO 480
      IF (EPRCP.LT.-TG1*HC1) GO TO 460
C**** RAIN HEATS UP TG1 TO FREEZING POINT AND MELTS SOME SNOW OR ICE
      DWATER=(TG1*HC1+EPRCP)/LHM
      TG1=0.
      RUN0=DWATER+PRCP
      IF (DWATER.GT.SNOW) GO TO 440
C**** RAIN MELTS SOME SNOW
      SNOW=SNOW-DWATER
      GO TO 580
C**** RAIN MELTS ALL SNOW AND SOME ICE, ICE MOVES UP THROUGH THE LAYERS
  440 DIFS=SNOW-DWATER
      SNOW=0.
      TG1=-TG2*DIFS/ACE1I
      EDIFS=DIFS*(TG2*SHI-LHM)
      ERUN2=EDIFS
      GO TO 560
C**** RAIN COOLS TO FREEZING POINT AND HEATS UP TG1
  460 TG1=TG1+EPRCP/HC1
      RUN0=PRCP
      GO TO 590
C**** SNOW INCREASES SNOW AMOUNT AND SNOW TEMPERATURE RECOMPUTES TG1
  480 TG1=(TG1*HC1+EPRCP)/(HC1+PRCP*SHI)
      SNOW=SNOW+PRCP
      IF (SNOW.LE.ACE1I) GO TO 580
C**** SNOW IS COMPACTED INTO ICE, ICE MOVES DOWN THROUGH THE LAYERS
      DIFS=SNOW-.9*ACE1I
      SNOW=.9*ACE1I
      EDIFS=DIFS*(TG1*SHI-LHM)
      ERUN2=DIFS*(TG2*SHI-LHM)
      GDATA(I,J,14)=TG2+(TG1-TG2)*DIFS/ACE2LI
  560    BEDIFS=BEDIFS+EDIFS*PLICE
         AIJ(I,J,IJ_F1LI)=AIJ(I,J,IJ_F1LI)+EDIFS
         BDIFS=BDIFS+DIFS*PLICE
         DIFSS=DIFSS+DIFS*PLICE
         BERUN2=BERUN2+ERUN2*PLICE
         AIJ(I,J,IJ_ERUN2)=AIJ(I,J,IJ_ERUN2)+ERUN2
         BRUN2=BRUN2+DIFS*PLICE
  580 GDATA(I,J,12)=SNOW
  590 GDATA(I,J,13)=TG1
         BRUN0=BRUN0+RUN0*PLICE
         RUN0S=RUN0S+RUN0*PLICE
         AIJ(I,J,IJ_RUNLI)=AIJ(I,J,IJ_RUNLI)+RUN0
C****
  600 IF (PEARTH.LE.0.) GO TO 940
C****
C**** EARTH  (all else is done in subroutine EARTH, called from SURFCE)
C****
         BENRGP=BENRGP+ENRGP*PEARTH
         AIJ(I,J,IJ_F0E)=AIJ(I,J,IJ_F0E)+ENRGP
C****
C**** ACCUMULATE DIAGNOSTICS (ocean, ocean ice, land ice only)
C****
  940    AREG(JR,39)=AREG(JR,39)+ENRGP*DXYPJ
         AREG(JR,45)=AREG(JR,45)+DIFSS*DXYPJ ! ocn/land ice contribution
         AREG(JR,54)=AREG(JR,54)+RUN0S*DXYPJ
         AIJ(I,J,IJ_PREC)=AIJ(I,J,IJ_PREC)+PREC(I,J)
         AIJ(I,J,IJ_NETH)=AIJ(I,J,IJ_NETH)+ENRGP
  960 CONTINUE
         AJ(J,39)=AJ(J,39)+AENRGP
         BJ(J,39)=BJ(J,39)+BENRGP
         CJ(J,39)=CJ(J,39)+CENRGP
C        BJ(J,40)=BJ(J,40)+BERUN0         ! land ice (Tg=0)
         BJ(J,41)=BJ(J,41)+BEDIFS         ! land ice contribution
         CJ(J,41)=CJ(J,41)+CEDIFS
         AJ(J,43)=AJ(J,43)+AEFO
         BJ(J,43)=BJ(J,43)+BERUN2         ! land ice contribution
         CJ(J,43)=CJ(J,43)+CERUN2
         BJ(J,45)=BJ(J,45)+BDIFS          ! land ice contribution
         CJ(J,45)=CJ(J,45)+CDIFS
         BJ(J,54)=BJ(J,54)+BRUN0          ! land ice contribution
         CJ(J,54)=CJ(J,54)+CRUN0
         AJ(J,46)=AJ(J,46)+AIFO
         BJ(J,46)=BJ(J,46)+BRUN2          ! land ice contribution
         CJ(J,46)=CJ(J,46)+CRUN2
         AJ(J,47)=AJ(J,47)+ARUN4
         CJ(J,47)=CJ(J,47)+CRUN4
         AJ(J,48)=AJ(J,48)+AERUN4
         CJ(J,48)=CJ(J,48)+CERUN4
  980 CONTINUE
      RETURN
      END
      SUBROUTINE COSZ0
C****
C**** calculates the Earth's zenith angle, weighted either
C**** by time or by sun light.
C****
      USE CONSTANT, only : grav,rgas,kapa,sday,lhm,lhe,lhs,twopi,omega
      USE E001M12_COM
      IMPLICIT NONE

      REAL*8, DIMENSION(IM) :: LT1,LT2,SLT1,SLT2,S2LT1,S2LT2
      REAL*8, DIMENSION(IM,JM) :: COSZ,COSZA
      REAL*8, DIMENSION(IM) :: RI,SINI,COSI
      REAL*8, DIMENSION(JM) :: SINJ,COSJ
      COMMON/WORK5/LT1,LT2,SLT1,SLT2,S2LT1,S2LT2
C**** ZERO1 HAS TO EQUAL THE CUT-OFF VALUE FOR COSZ USED IN SOLAR
C**** COSZS WORKS CORRECTLY ONLY IF ZERO1 >> 1.D-3
      REAL*8 ZERO1
      DATA ZERO1/1.D-2/
      INTEGER I,J,L
      REAL*8 PHIS,PSHIS,CPHIS,SPHIS,PHIN,SPHIN,CPHIN,PHIM,S2DAWN,S2DUSK
     *     ,ECOSZ,ECOSQZ,CLT1,CLT2,ZERO2,CDUSK,DUSK,DAWN,SDUSK,SDAWN
     *     ,CJCD,SJSD,SR1,CR1,SR2,CR2,ROT1,ROT2,DROT 

C**** COMPUTE THE AREA WEIGHTED LATITUDES AND THEIR SINES AND COSINES
      PHIS=-.25*TWOPI
      SPHIS=-1.
      CPHIS=0.
      DO 20 J=1,JM-1
      PHIN=(TWOPI/(2*JM-2))*(J-.5*JM)
      SPHIN=SIN(PHIN)
      CPHIN=COS(PHIN)
      PHIM=(PHIN*SPHIN+CPHIN-PHIS*SPHIS-CPHIS)/(SPHIN-SPHIS)
      SINJ(J)=SIN(PHIM)
      COSJ(J)=COS(PHIM)
      PHIS=PHIN
      SPHIS=SPHIN
   20 CPHIS=CPHIN
      PHIN=.25*TWOPI
      SPHIN=1.
      CPHIN=0.
      PHIM=(PHIN*SPHIN+CPHIN-PHIS*SPHIS-CPHIS)/(SPHIN-SPHIS)
      SINJ(JM)=SIN(PHIM)
      COSJ(JM)=COS(PHIM)
C**** COMPUTE THE SINES AND COSINES OF LONGITUDE
      DO 40 I=1,IM
      RI(I)=(TWOPI/IM)*(I-.5)
      SINI(I)=SIN(RI(I))
   40 COSI(I)=COS(RI(I))
      RETURN
C****
C****
      ENTRY COSZT (ROT1,ROT2,COSZ)
C****
C**** THIS ENTRY COMPUTES THE ZENITH ANGLE WEIGHTED BY DAYTIME
C**** HOURS FROM ROT1 TO ROT2, GREENWICH MEAN TIME IN RADIANS.  ROT1
C**** MUST BE BETWEEN 0 AND 2*PI.  ROT2 MUST BE BETWEEN ROT1 AND
C**** ROT1+2*PI.  I=1 MUST LIE ON THE INTERNATIONAL DATE LINE.
C****
      DROT=ROT2-ROT1
C**** COMPUTE THE SINES AND COSINES OF THE INITIAL AND FINAL GMT'S
  100 SR1=SIN(ROT1)
      CR1=COS(ROT1)
      SR2=SIN(ROT2)
      CR2=COS(ROT2)
C**** COMPUTE THE INITIAL AND FINAL LOCAL TIMES (MEASURED FROM NOON TO
C****   NOON) AND THEIR SINES AND COSINES
      DO 120 I=1,IM
      LT1(I)=ROT1+RI(I)
      SLT1(I)=SR1*COSI(I)+CR1*SINI(I)
      LT2(I)=ROT2+RI(I)
  120 SLT2(I)=SR2*COSI(I)+CR2*SINI(I)
C****
C**** CALCULATION FOR POLAR GRID BOXES
C****
      DO 200 J=1,JM,JM-1
      SJSD=SINJ(J)*SIND
      CJCD=COSJ(J)*COSD
      IF (SJSD+CJCD.LE.ZERO1) GO TO 180
      IF (SJSD-CJCD.GE.0.) GO TO 160
C**** AVERAGE COSZ FROM DAWN TO DUSK NEAR THE POLES
      DUSK=ACOS(-SJSD/CJCD)
      SDUSK=SQRT(CJCD*CJCD-SJSD*SJSD)/CJCD
      DAWN=-DUSK
      SDAWN=-SDUSK
      COSZ(1,J)=(SJSD*(DUSK-DAWN)+CJCD*(SDUSK-SDAWN))/TWOPI
      GO TO 200
C**** CONSTANT DAYLIGHT NEAR THE POLES
  160 COSZ(1,J)=SJSD
      GO TO 200
C**** CONSTANT NIGHTIME NEAR THE POLES
  180 COSZ(1,J)=0.
  200 CONTINUE
C****
C**** LOOP OVER NON-POLAR LATITUDES
C****
      DO 500 J=2,JM-1
      SJSD=SINJ(J)*SIND
      CJCD=COSJ(J)*COSD
      IF (SJSD+CJCD.LE.ZERO1) GO TO 460
      IF (SJSD-CJCD.GE.0.) GO TO 420
C**** COMPUTE DAWN AND DUSK (AT LOCAL TIME) AND THEIR SINES
      DUSK=ACOS(-SJSD/CJCD)
      SDUSK=SQRT(CJCD*CJCD-SJSD*SJSD)/CJCD
      DAWN=-DUSK
      SDAWN=-SDUSK
C**** NEITHER CONSTANT DAYTIME NOR CONSTANT NIGHTIME AT THIS LATITUDE,
C**** LOOP OVER LONGITUDES
      ZERO2=ZERO1/CJCD
      DO 400 I=1,IM
C**** FORCE DUSK TO LIE BETWEEN LT1 AND LT1+2*PI
      IF (DUSK.GT.LT1(I)+ZERO2) GO TO 220
      DUSK=DUSK+TWOPI
      DAWN=DAWN+TWOPI
  220 IF (DAWN.LT.LT2(I)-ZERO2) GO TO 240
C**** CONTINUOUS NIGHTIME FROM INITIAL TO FINAL TIME
      COSZ(I,J)=0.
      GO TO 400
  240 IF (DAWN.GE.LT1(I)) GO TO 300
      IF (DUSK.LT.LT2(I)) GO TO 260
C**** CONTINUOUS DAYLIGHT FROM INITIAL TIME TO FINAL TIME
      COSZ(I,J)=SJSD+CJCD*(SLT2(I)-SLT1(I))/DROT
      GO TO 400
  260 IF (DAWN+TWOPI.LT.LT2(I)-ZERO2) GO TO 280
C**** DAYLIGHT AT INITIAL TIME AND NIGHT AT FINAL TIME
      COSZ(I,J)=(SJSD*(DUSK-LT1(I))+CJCD*(SDUSK-SLT1(I)))/DROT
      GO TO 400
C**** DAYLIGHT AT INITIAL AND FINAL TIMES WITH NIGHTIME IN BETWEEN
  280 COSZ(I,J)=(SJSD*(LT2(I)-DAWN-TWOPI+DUSK-LT1(I))+CJCD*
     *  (SLT2(I)-SDAWN+SDUSK-SLT1(I)))/DROT
      GO TO 400
  300 IF (DUSK.LT.LT2(I)) GO TO 320
C**** NIGHT AT INITIAL TIME AND DAYLIGHT AT FINAL TIME
      COSZ(I,J)=(SJSD*(LT2(I)-DAWN)+CJCD*(SLT2(I)-SDAWN))/DROT
      GO TO 400
C**** NIGHTIME AT INITIAL AND FINAL TIMES WITH DAYLIGHT IN BETWEEN
  320 COSZ(I,J)=(SJSD*(DUSK-DAWN)+CJCD*(SDUSK-SDAWN))/DROT
  400 CONTINUE
      GO TO 500
C**** CONSTANT DAYLIGHT AT THIS LATITUDE
  420 DO 440 I=1,IM
  440 COSZ(I,J)=SJSD+CJCD*(SLT2(I)-SLT1(I))/DROT
      GO TO 500
C**** CONSTANT NIGHTIME AT THIS LATITUDE
  460 DO 480 I=1,IM
  480 COSZ(I,J)=0.
  500 CONTINUE
      RETURN
C****
C****
      ENTRY COSZS (ROT1,ROT2,COSZ,COSZA)
C****
C**** THIS ENTRY COMPUTES THE ZENITH ANGLE TWICE, FIRST WEIGHTED BY THE
C**** DAYTIME HOURS FROM ROT1 TO ROT2 AND SECONDLY WEIGHTED BY THE
C**** INCIDENT SUN LIGHT FROM ROT1 TO ROT2.  COSZT MUST HAVE BEEN
C**** CALLED JUST PREVIOUSLY.
C****
      DROT=ROT2-ROT1
C**** COMPUTE THE SINES AND COSINES OF THE INITIAL AND FINAL GMT'S
      SR1=SIN(ROT1)
      CR1=COS(ROT1)
      SR2=SIN(ROT2)
      CR2=COS(ROT2)
C**** COMPUTE THE INITIAL AND FINAL LOCAL TIMES (MEASURED FROM NOON TO
C****   NOON) AND THEIR SINES AND COSINES
      DO 520 I=1,IM
      LT1(I)=ROT1+RI(I)
      SLT1(I)=SR1*COSI(I)+CR1*SINI(I)
      CLT1=CR1*COSI(I)-SR1*SINI(I)
      S2LT1(I)=2.*SLT1(I)*CLT1
      LT2(I)=ROT2+RI(I)
      SLT2(I)=SR2*COSI(I)+CR2*SINI(I)
      CLT2=CR2*COSI(I)-SR2*SINI(I)
  520 S2LT2(I)=2.*SLT2(I)*CLT2
C****
C**** CALCULATION FOR POLAR GRID BOXES
C****
      DO 600 J=1,JM,JM-1
      SJSD=SINJ(J)*SIND
      CJCD=COSJ(J)*COSD
      IF (SJSD+CJCD.LE.ZERO1) GO TO 580
      IF (SJSD-CJCD.GE.0.) GO TO 560
C**** AVERAGE COSZ FROM DAWN TO DUSK NEAR THE POLES
      CDUSK=-SJSD/CJCD
      DUSK=ACOS(CDUSK)
      SDUSK=SQRT(CJCD*CJCD-SJSD*SJSD)/CJCD
      S2DUSK=2.*SDUSK*CDUSK
      DAWN=-DUSK
      SDAWN=-SDUSK
      S2DAWN=-S2DUSK
      ECOSZ=SJSD*(DUSK-DAWN)+CJCD*(SDUSK-SDAWN)
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SDUSK-SDAWN)+
     *  .5*CJCD*(DUSK-DAWN+.5*(S2DUSK-S2DAWN)))
      COSZ(1,J)=ECOSZ/TWOPI
      COSZA(1,J)=ECOSQZ/ECOSZ
      GO TO 600
C**** CONSTANT DAYLIGHT NEAR THE POLES
  560 ECOSZ=SJSD*TWOPI
      ECOSQZ=SJSD*ECOSZ+.5*CJCD*CJCD*TWOPI
      COSZ(1,J)=ECOSZ/TWOPI
      COSZA(1,J)=ECOSQZ/ECOSZ
      GO TO 600
C**** CONSTANT NIGHTIME NEAR THE POLES
  580 COSZ(1,J)=0.
      COSZA(1,J)=0.
  600 CONTINUE
C****
C**** LOOP OVER NON-POLAR LATITUDES
C****
      DO 900 J=2,JM-1
      SJSD=SINJ(J)*SIND
      CJCD=COSJ(J)*COSD
      IF (SJSD+CJCD.LE.ZERO1) GO TO 860
      IF (SJSD-CJCD.GE.0.) GO TO 820
C**** COMPUTE DAWN AND DUSK (AT LOCAL TIME) AND THEIR SINES
      CDUSK=-SJSD/CJCD
      DUSK=ACOS(CDUSK)
      SDUSK=SQRT(CJCD*CJCD-SJSD*SJSD)/CJCD
      S2DUSK=2.*SDUSK*CDUSK
      DAWN=-DUSK
      SDAWN=-SDUSK
      S2DAWN=-S2DUSK
C**** NEITHER CONSTANT DAYTIME NOR CONSTANT NIGHTIME AT THIS LATITUDE,
C**** LOOP OVER LONGITUDES
      ZERO2=ZERO1/CJCD
      DO 800 I=1,IM
C**** FORCE DUSK TO LIE BETWEEN LT1 AND LT1+2*PI
      IF (DUSK.GT.LT1(I)+ZERO2) GO TO 620
      DUSK=DUSK+TWOPI
      DAWN=DAWN+TWOPI
  620 IF (DAWN.LT.LT2(I)-ZERO2) GO TO 640
C**** CONTINUOUS NIGHTIME FROM INITIAL TO FINAL TIME
      COSZ(I,J)=0.
      COSZA(I,J)=0.
      GO TO 800
  640 IF (DAWN.GE.LT1(I)) GO TO 700
      IF (DUSK.LT.LT2(I)) GO TO 660
C**** CONTINUOUS DAYLIGHT FROM INITIAL TIME TO FINAL TIME
      ECOSZ=SJSD*DROT+CJCD*(SLT2(I)-SLT1(I))
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SLT2(I)-SLT1(I))+
     *  .5*CJCD*(DROT+.5*(S2LT2(I)-S2LT1(I))))
      COSZ(I,J)=ECOSZ/DROT
      COSZA(I,J)=ECOSQZ/ECOSZ
      GO TO 800
  660 IF (DAWN+TWOPI.LT.LT2(I)-ZERO2) GO TO 680
C**** DAYLIGHT AT INITIAL TIME AND NIGHT AT FINAL TIME
      ECOSZ=SJSD*(DUSK-LT1(I))+CJCD*(SDUSK-SLT1(I))
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SDUSK-SLT1(I))+
     *  .5*CJCD*(DUSK-LT1(I)+.5*(S2DUSK-S2LT1(I))))
      COSZ(I,J)=ECOSZ/DROT
      COSZA(I,J)=ECOSQZ/ECOSZ
      GO TO 800
C**** DAYLIGHT AT INITIAL AND FINAL TIMES WITH NIGHTIME IN BETWEEN
  680 ECOSZ=SJSD*(DROT-DAWN-TWOPI+DUSK)+
     *  CJCD*(SLT2(I)-SDAWN+SDUSK-SLT1(I))
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SDUSK-SLT1(I)+SLT2(I)-SDAWN)+
     *  .5*CJCD*(DUSK+DROT-DAWN-TWOPI+
     *  .5*(S2DUSK-S2LT1(I)+S2LT2(I)-S2DAWN)))
      COSZ(I,J)=ECOSZ/DROT
      COSZA(I,J)=ECOSQZ/ECOSZ
      GO TO 800
  700 IF (DUSK.LT.LT2(I)) GO TO 720
C**** NIGHT AT INITIAL TIME AND DAYLIGHT AT FINAL TIME
      ECOSZ=SJSD*(LT2(I)-DAWN)+CJCD*(SLT2(I)-SDAWN)
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SLT2(I)-SDAWN)+
     *  .5*CJCD*(LT2(I)-DAWN+.5*(S2LT2(I)-S2DAWN)))
      COSZ(I,J)=ECOSZ/DROT
      COSZA(I,J)=ECOSQZ/ECOSZ
      GO TO 800
C**** NIGHTIME AT INITIAL AND FINAL TIMES WITH DAYLIGHT IN BETWEEN
  720 ECOSZ=SJSD*(DUSK-DAWN)+CJCD*(SDUSK-SDAWN)
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SDUSK-SDAWN)+
     *  .5*CJCD*(DUSK-DAWN+.5*(S2DUSK-S2DAWN)))
      COSZ(I,J)=ECOSZ/DROT
      COSZA(I,J)=ECOSQZ/ECOSZ
  800 CONTINUE
      GO TO 900
C**** CONSTANT DAYLIGHT AT THIS LATITUDE
  820 DO 840 I=1,IM
      ECOSZ=SJSD*DROT+CJCD*(SLT2(I)-SLT1(I))
      ECOSQZ=SJSD*ECOSZ+CJCD*(SJSD*(SLT2(I)-SLT1(I))+
     *  .5*CJCD*(DROT+.5*(S2LT2(I)-S2LT1(I))))
      COSZ(I,J)=ECOSZ/DROT
  840 COSZA(I,J)=ECOSQZ/ECOSZ
      GO TO 900
C**** CONSTANT NIGHTIME AT THIS LATITUDE
  860 DO 880 I=1,IM
      COSZ(I,J)=0.
  880 COSZA(I,J)=0.
  900 CONTINUE
      RETURN
      END
      SUBROUTINE RADIA
C****
C**** THIS SUBROUTINES ADDS THE RADIATION HEATING TO THE TEMPERATURES
C****
      USE CONSTANT, only : grav,rgas,kapa,sday,lhm,lhe,lhs,twopi,omega
     *     ,tf
      USE E001M12_COM
      USE GEOM, Jdummy=>JLAT
      USE RADNCB, only : RQT,SRHR,TRHR,FSF,COSZ1,S0X,CO2
      USE RE001
     &  , only : setnew,rcomp1,writer,rcompx,rcompt ! routines
     &             ,lx
     &             ,FULGAS ,PTLISO ,KTREND ,LMR=>NL ,LMRP=>NLP
C     INPUT DATA
     &             ,PLE=>PLB ,TL=>TLM ,QL=>SHL
     &             ,TAUWC ,TAUIC ,SIZEWC ,SIZEIC
     &             ,POCEAN,PEARTH,POICE,PLICE,AGESN,SNOWE,SNOWOI,SNOWLI
     &             ,TGO,TGE,TGOI,TGLI,TS=>TSL,WS=>WMAG,WEARTH,PTOPTR
     &             ,S00WM2,RATLS0,S0,COSZ,PVT
     &             ,JYEARR=>JYEAR,JDAYR=>JDAY,JLAT,ILON,KCLDEM,KVEGA6
C     OUTPUT DATA
     &             ,TRDFLB ,TRNFLB ,TRFCRL
     &             ,SRDFLB ,SRNFLB ,SRFHRL
     &             ,PLAVIS ,PLANIR ,ALBVIS ,ALBNIR ,FSRNFG
     &             ,SRRVIS ,SRAVIS ,SRRNIR ,SRANIR
     &             ,BTEMPW
C     Some Aerosol Control Parameters not currently referenced
c    &             ,QXAERO ,QSAERO ,QCAERO ,ATAERO
c    &             ,QAER55 ,REAERO ,VEAERO ,ROAERO ,PI0MAX
c    &             ,FSAERO ,FTAERO ,VDGAER ,SSBTAU ,PIAERO
      USE RANDOM
      USE CLD01_COM_E001, only : TAUSS,TAUMC,SVLHX,RHSAV,SVLAT,CLDSAV,
     *     CLDSS,CLDMC,CSIZE
      USE PBLCOM, only : wsavg,tsavg
      USE DAGCOM  !, only : aj,bj,cj,areg,jreg,aij,ail,ajl,asjl,adaily,
!     *     iwrite,jwrite,itwrite
      USE DYNAMICS, only : pk,pedn
      USE OCEAN, only : odata
      USE FILEMANAGER

      IMPLICIT NONE

      REAL*8, DIMENSION(IM,JM) :: COSZ2,COSZA,TRINCG,BTMPW
      REAL*8, DIMENSION(IM,JM,4) :: SNFS,TNFS
      REAL*8, DIMENSION(IM,JM,3) :: TRHRS,SRHRS
      REAL*8, DIMENSION(IM,JM,9) :: ALB
      COMMON/WORK1d/COSZ2,COSZA,TRINCG,BTMPW,SNFS,TNFS,TRHRS,SRHRS,ALB

      REAL*8, DIMENSION(LM) :: TOTCLD
      COMMON/WORK2c/ TOTCLD

      REAL*8, DIMENSION(LM+3) :: COE

      REAl*8 TCIR,STBO
      DATA TCIR/258.16/,STBO/.567257D-7/
      INTEGER :: IFIRST = 1, JDLAST = -9
      INTEGER I,J,L,K,KR,LR,JYFIX,JDFIX,MADVEL,INCHM,J50N,J70N,LLOW,LMID
     *     ,LHI,IHOUR,IMAX,IM1,JR,IH,INCH,LMID1,LHI1
      REAL*8 DTCNDS,COEX,CO2REF,ROT1,ROT2,PLAND,PIJ,RANDSS,RANDMC,CSS
     *     ,CMC,DEPTH,QSS,TAUSSL,TAUMCL,ELHX,CLDCV,DXYPJ,ASRHR,ATRHR
     *     ,ASNFS1,BSNFS1,CSNFS1,ATNFS1,BTNFS1,CTNFS1,SRNFLG,X
!@var NRFUN indices of unit numbers for radiation routines
      INTEGER NRFUN(14),IU
!@var RUNSTR names of files for radiation routines
      CHARACTER*5 :: RUNSTR(14) = (/"RADN1","RADN2","RADN3",
     *     "RADN4","RADN5","RADN6","RADN7","RADN8",
     *     "RADN9","RADNA","RADNB","RADNC","RADND",
     *     "RADNE"/)
!@var QBIN true if files for radiation routines are binary
      LOGICAL :: QBIN(14) = (/.T.,.T.,.F.,.F.,.T.,.T.,.T.,.T.,.F.,.T.,
     *     .T.,.T.,.T.,.T./)
C****
C**** FLAND     LAND COVERAGE (1)
C**** FLICE     LAND ICE COVERAGE (1)
C****
C**** ODATA  1  OCEAN TEMPERATURE (C)
C****        2  RATIO OF OCEAN ICE COVERAGE TO WATER COVERAGE (1)
C****
C**** GDATA  1  OCEAN ICE SNOW AMOUNT (KG/M**2)
C****        2  EARTH SNOW AMOUNT (KG/M**2)
C****        3  OCEAN ICE TEMPERATURE OF FIRST LAYER (C)
C****        4  EARTH TEMPERATURE OF FIRST LAYER (C)
C****        5  EARTH WATER OF FIRST LAYER (KG/M**2)
C****        6  EARTH ICE OF FIRST LAYER (KG/M**2)
C****        9  AGE OF SNOW OVER OCEAN ICE (DAYS)
C****       10  AGE OF SNOW OVER LAND ICE (DAYS)
C****       11  AGE OF SNOW OVER EARTH (DAYS)
C****       12  LAND ICE SNOW AMOUNT (KG/M**2)
C****       13  LAND ICE TEMPERATURE OF FIRST LAYER (C)
C****
C****
C**** VDATA  1-11 RATIOS FOR THE 11 VEGETATION TYPES (1)
C****
      REAL*8 QSAT,TM,PR,QLH
      QSAT(TM,PR,QLH)=3.797915*DEXP(QLH*(7.93252D-6-2.166847D-3/TM))/PR
         IF (MODRD.EQ.0) IDACC(2)=IDACC(2)+1
      IF (IFIRST.NE.1) GO TO 50
      IFIRST=0
      CALL COSZ0
      DTCNDS=NCNDS*DT
C**** SET THE CONTROL PARAMETERS FOR THE RADIATION (need mean pressures)
      LMR=LM+3
      LMRP=LMR+1
      COEX=.01*GRAV*KAPA/RGAS
      DO L=1,LM
         COE(L)=DTCNDS*COEX/DSIG(L)
         PLE(L)=SIGE(L)*(PSF-PTOP)+PTOP
      END DO
      PLE(LM+1)=SIGE(LM+1)*(PSF-PTOP)+PTOP
      PLE(LM+2)=.5*PLE(LM+1)
      PLE(LMR)=.2*PLE(LM+1)
      PLE(LMR+1)=1.D-5
      PTOPTR=PTOP ! top of sigma-coord.system
      DO 40 LR=LM+1,LMR
   40 COE(LR)=DT*NRAD*COEX/(PLE(LR)-PLE(LR+1))
      PTLISO=15.
      IF(CO2.LT.0.) KTREND=-NINT(CO2)
C**** Default: time-dependent So/GHG/O3/Trop-Aeros/Dust/Volc-Aeros
C****     For control runs e.g. with Jul 1,1951 atmosphere use
          JYFIX=1951
          JDFIX=182   !  Julian date (if JDFIX=0, annual cycle is used)
        CALL SETNEW(11,JYFIX,JDFIX, 1,0,0.D0) ! fix sol.const. - KSOLAR
        CALL SETNEW( 2,JYFIX,JDFIX, 0,0,0.D0) ! fix GHG (trend KTREND)
        CALL SETNEW(13,0    ,0    , 0,0,0.D0) ! no GHG-resets - MADGAS
        CALL SETNEW( 3,JYFIX,0    , 0,0,0.D0) ! quasi-fix O3 (ann.cycle)
        CALL SETNEW( 4,JYFIX,0    , 0,0,0.D0) ! quasi-fix Trop. Aerosols
        CALL SETNEW( 5,JYFIX,0    , 0,0,0.D0) ! quasi-fix Desert Dust
        CALL SETNEW( 6,JYFIX,JDFIX, 0,0,0.D0) ! fix Volc. Aerosols
C     An annual cycle is used for the data below, to prevent this, use
CnoAC   CALL SETNEW(7, 0,JDFIX, 0,0,0.D0) ! cloud heterogeneity - KCLDEP
CnoAC   CALL SETNEW(8, 0,JDFIX, 0,0,0.D0) !  surface albedo
C**** New options (currently not used)
      KCLDEM=0  ! 0:old 1:new LW cloud scattering scheme  -  KCLDEM
      KVEGA6=0  ! 0:2-band 1:6-band veg.albedo            -  KVEGA6
      MADVEL=123456         ! suppress reading i-th time series by i->0
C**** set up unit numbers for 14 radiation input files
      DO IU=1,14
         IF (IU.EQ.12.OR.IU.EQ.13) CYCLE    ! not used in GCM
         call getunit(RUNSTR(IU),NRFUN(IU),QBIN(IU))   
      END DO
      CALL RCOMP1 (MADVEL,NRFUN) ! 1=O3 2=TrAer 3=Dust 4=VAer 5=Clds 6=SoUV
      CO2REF=FULGAS(2)
      IF(CO2.GE.0.) FULGAS(2)=CO2REF*CO2
         CALL WRITER (6,0)
         INCHM=NRAD/NDYN
c         JEQ=1+JM/2
         J50N=(50.+90.)*(JM-1)/180.+1.5
         J70N=(70.+90.)*(JM-1)/180.+1.5
C**** CLOUD LAYER INDICES USED FOR DIAGNOSTICS
         DO 43 L=1,LM
         LLOW=L
         IF (.5*(PLE(L+1)+PLE(L+2)).LT.750.) GO TO 44 ! was 786. 4/16/97
   43    CONTINUE
   44    LMID1=LLOW+1
         DO 45 L=LMID1,LM
         LMID=L
         IF (.5*(PLE(L+1)+PLE(L+2)).LT.430.) GO TO 46
   45    CONTINUE
   46    LHI1=LMID+1
         LHI=LM
         IF (LHI1.GT.LHI) LHI=LHI1
         WRITE (6,47) LLOW,LMID1,LMID,LHI1,LHI
   47    FORMAT (' LOW CLOUDS IN LAYERS 1-',I2,'   MID LEVEL CLOUDS IN',
     *     ' LAYERS',I3,'-',I2,'   HIGH CLOUDS IN LAYERS',I3,'-',I2)
C**** Calculate mean cosine of zenith angle for the current physics step
   50 CONTINUE
      ROT1=TWOPI*TOFDAY/24.
      ROT2=ROT1+TWOPI*DTCNDS/SDAY
      CALL COSZT (ROT1,ROT2,COSZ1)
         IHOUR=1.5+TOFDAY
      IF (MODRD.NE.0) GO TO 900
C****
C**** Interface with radiation routines, done only every NRAD time steps
C****
C**** Calculate mean cosine of zenith angle for the full radiation step
      ROT2=ROT1+TWOPI*NRAD*DT/SDAY
      CALL COSZS (ROT1,ROT2,COSZ2,COSZA)
C****
C**** COMPUTE EARTH ALBEDOS AND OTHER PARAMETERS FOR BEGINNING OF DAY
C****
      JDAYR=JDAY
      JYEARR=JYEAR
      IF(JDAY.NE.JDLAST) CALL RCOMPT
      S0=S0X*S00WM2*RATLS0/RSDIST
      JDLAST=JDAY
C****
C**** MAIN J LOOP
C****
      DO 600 J=1,JM
      IMAX=IMAXJ(J) 
      JLAT=NINT(1.+(J-1.)*45./(JM-1.))  !  j w.r.to 72x46 grid
C****
C**** MAIN I LOOP
C****
      IM1=IM
      DO 500 I=1,IMAX
         JR=JREG(I,J)
C**** DETERMINE FRACTIONS FOR SURFACE TYPES AND COLUMN PRESSURE
      PLAND=FLAND(I,J)
      POICE=ODATA(I,J,2)*(1.-PLAND)
      POCEAN=(1.-PLAND)-POICE
      PLICE=FLICE(I,J)
      PEARTH=FEARTH(I,J)
      PIJ=P(I,J)
C****
C**** DETERMINE CLOUDS (AND THEIR OPTICAL DEPTHS) SEEN BY RADIATION
C****
      RANDSS=RANDU(X)
      RANDMC=RANDU(X)
         CSS=0.
         CMC=0.
         DEPTH=0.
      DO 240 L=1,LM
      IF(L.EQ.LS1)  PIJ=PSF-PTOP
      QSS=Q(I,J,L)/(RHSAV(I,J,L)+1.D-20)
      QL(L)=QSS
      IF(CLDSAV(I,J,L).LT.1.)
     *  QL(L)=(Q(I,J,L)-QSS*CLDSAV(I,J,L))/(1.-CLDSAV(I,J,L))
      TL(L)=T(I,J,L)*PK(L,I,J)
      IF(CLDSS(I,J,L).EQ.0.) RANDSS=RANDU(X)
      TAUSSL=0.
      TAUMCL=0.
      TAUWC(L)=0.
      TAUIC(L)=0.
      SIZEWC(L)=CSIZE(I,J,L,1)
      SIZEIC(L)=CSIZE(I,J,L,1)
         TOTCLD(L)=0.
      IF (CLDSS(I,J,L).LT.RANDSS.OR.TAUSS(I,J,L).LE.0.) GO TO 220
      TAUSSL=TAUSS(I,J,L)
      QL(L)=QSS
         CSS=1.
         AJL(J,L,28)=AJL(J,L,28)+CSS
         TOTCLD(L)=1.
  220 IF (CLDMC(I,J,L).LT.RANDMC.OR.TAUMC(I,J,L).LE.0.) GO TO 230
         CMC=1.
         AJL(J,L,29)=AJL(J,L,29)+CMC
         TOTCLD(L)=1.
         DEPTH=DEPTH+PIJ*DSIG(L)
      IF(TAUMC(I,J,L).LE.TAUSSL) GO TO 230
      TAUMCL=TAUMC(I,J,L)
      ELHX=LHE
      IF(TL(L).LE.TF) ELHX=LHS
      QL(L)=QSAT(TL(L),SIG(L)*PIJ+PTOP,ELHX)
  230    AJL(J,L,19)=AJL(J,L,19)+TOTCLD(L)
      IF(TAUSSL+TAUMCL.GT.0.) THEN
         IF(TAUMCL.GT.TAUSSL) THEN
           IF(SVLAT(I,J,L).EQ.LHE) THEN
             TAUWC(L)=TAUMCL
           ELSE
             TAUIC(L)=TAUMCL
           END IF
         ELSE
           IF(SVLHX(I,J,L).EQ.LHE) THEN
             TAUWC(L)=TAUSSL
             SIZEWC(L)=CSIZE(I,J,L,2)
           ELSE
             TAUIC(L)=TAUSSL
             SIZEIC(L)=CSIZE(I,J,L,2)
           END IF
         END IF
      END IF
  240 CONTINUE
         PIJ=P(I,J)
         AJ(J,57)=AJ(J,57)+CSS*POCEAN
         BJ(J,57)=BJ(J,57)+CSS*PLAND
         CJ(J,57)=CJ(J,57)+CSS*POICE
         AREG(JR,57)=AREG(JR,57)+CSS*DXYP(J)
         AJ(J,58)=AJ(J,58)+CMC*POCEAN
         BJ(J,58)=BJ(J,58)+CMC*PLAND
         CJ(J,58)=CJ(J,58)+CMC*POICE
         AREG(JR,58)=AREG(JR,58)+CMC*DXYP(J)
         AIJ(I,J,IJ_PMCCLD)=AIJ(I,J,IJ_PMCCLD)+CMC
         AJ(J,80)=AJ(J,80)+DEPTH*POCEAN
         BJ(J,80)=BJ(J,80)+DEPTH*PLAND
         CJ(J,80)=CJ(J,80)+DEPTH*POICE
         AREG(JR,80)=AREG(JR,80)+DEPTH*DXYP(J)
         CLDCV=CMC+CSS-CMC*CSS
         AJ(J,59)=AJ(J,59)+CLDCV*POCEAN
         BJ(J,59)=BJ(J,59)+CLDCV*PLAND
         CJ(J,59)=CJ(J,59)+CLDCV*POICE
         AREG(JR,59)=AREG(JR,59)+CLDCV*DXYP(J)
         AIJ(I,J,IJ_CLDCV)=AIJ(I,J,IJ_CLDCV)+CLDCV
         DO 250 L=1,LLOW
         IF (TOTCLD(L).NE.1.) GO TO 250
         AIJ(I,J,IJ_PCLDL)=AIJ(I,J,IJ_PCLDL)+1.
         GO TO 255
  250    CONTINUE
  255    DO 260 L=LMID1,LMID
         IF (TOTCLD(L).NE.1.) GO TO 260
         AIJ(I,J,IJ_PCLDM)=AIJ(I,J,IJ_PCLDM)+1.
         GO TO 265
  260    CONTINUE
  265    DO 270 L=LHI1,LHI
         IF (TOTCLD(L).NE.1.) GO TO 270
         AIJ(I,J,IJ_PCLDH)=AIJ(I,J,IJ_PCLDH)+1.
         GO TO 275
  270    CONTINUE
  275    CONTINUE
         PIJ=PSF-PTOP
         DO 280 L=LM,1,-1
         IF(L.EQ.LS1-1) PIJ=P(I,J)
         IF (TOTCLD(L).NE.1.) GO TO 280
         AIJ(I,J,IJ_CLDTPPR)=AIJ(I,J,IJ_CLDTPPR)+SIGE(L+1)*PIJ+PTOP
         GO TO 285
  280    CONTINUE
  285    DO KR=1,NDLYPT
            IF (I.EQ.IJD6(1,KR).AND.J.EQ.IJD6(2,KR)) THEN
               IH=IHOUR
               DO INCH=1,INCHM
                  IF (IH.GT.24) IH=IH-24
                  ADAILY(IH,21,KR)=ADAILY(IH,21,KR)+TOTCLD(6)
                  ADAILY(IH,22,KR)=ADAILY(IH,22,KR)+TOTCLD(5)
                  ADAILY(IH,23,KR)=ADAILY(IH,23,KR)+TOTCLD(4)
                  ADAILY(IH,24,KR)=ADAILY(IH,24,KR)+TOTCLD(3)
                  ADAILY(IH,25,KR)=ADAILY(IH,25,KR)+TOTCLD(2)
                  ADAILY(IH,26,KR)=ADAILY(IH,26,KR)+TOTCLD(1)
                  ADAILY(IH,27,KR)=ADAILY(IH,27,KR)+CLDCV
                  ADAILY(IH,53,KR)=ADAILY(IH,53,KR)+TOTCLD(7)
                  ADAILY(IH,54,KR)=ADAILY(IH,54,KR)+TOTCLD(6)
                  ADAILY(IH,55,KR)=ADAILY(IH,55,KR)+TOTCLD(5)
                  ADAILY(IH,56,KR)=ADAILY(IH,56,KR)+TOTCLD(4)
                  ADAILY(IH,57,KR)=ADAILY(IH,57,KR)+TOTCLD(3)
                  ADAILY(IH,58,KR)=ADAILY(IH,58,KR)+TOTCLD(2)
                  ADAILY(IH,59,KR)=ADAILY(IH,59,KR)+TOTCLD(1)
                  ADAILY(IH,61,KR)=ADAILY(IH,61,KR)+CLDCV
                  IH=IH+1
               END DO
            END IF
         END DO
C****
  300 CONTINUE
C****
C**** SET UP VERTICAL ARRAYS OMITTING THE I AND J INDICES
C****
C**** EVEN PRESSURES
      DO 340 L=1,LM
      PLE(L)=PEDN(L,I,J)
C**** TEMPERATURES
C---- TL(L)=T(I,J,L)*PK(L,I,J)     ! already defined
      IF(TL(L).LT.130..OR.TL(L).GT.370.) THEN
         WRITE(99,*) 'In Radia: TAU,I,J,L,TL',TAU,I,J,L,TL(L)
         STOP 'In Radia: Temperature out of range'
      END IF
C**** MOISTURE VARIABLES
C---- QL(L)=Q(I,J,L)        ! already defined
  340 CONTINUE
C****
C**** RADIATION, SOLAR AND THERMAL
C****
      DO 420 K=1,LM_REQ
      IF(RQT(I,J,K).LT.130..OR.RQT(I,J,K).GT.370.) THEN
         WRITE(99,*) 'In Radia: TAU,I,J,L,TL',TAU,I,J,LM+K,RQT(I,J,K)
         STOP 'In Radia: RQT out of range'
      END IF
  420 TL(LM+K)=RQT(I,J,K)
      COSZ=COSZA(I,J)
      TGO=ODATA(I,J,1)+TF
      TGOI=GDATA(I,J,3)+TF
      TGLI=GDATA(I,J,13)+TF
      TGE=GDATA(I,J,4)+TF
      TS=TSAVG(I,J)
      SNOWOI=GDATA(I,J,1)
      SNOWLI=GDATA(I,J,12)
      SNOWE=GDATA(I,J,2)
      AGESN(1)=GDATA(I,J,11)    ! land
      AGESN(2)=GDATA(I,J,9)     ! ocean ice
      AGESN(3)=GDATA(I,J,10)    ! land ice
      WEARTH=(GDATA(I,J,5)+GDATA(I,J,6))/(WFCS(I,J)+1.D-20)
      DO 430 K=1,11
  430 PVT(K)=VDATA(I,J,K)
      WS=WSAVG(I,J)
C-OLD FGOLDU(2)=XFRADJ*(1.-PEARTH)
C-OLD FGOLDU(3)=XFRADJ*PEARTH
      ILON=NINT(.5+(I-.5)*72./IM)
      CALL RCOMPX
      FSF(I,J,1)=FSRNFG(1)   !  ocean
      FSF(I,J,2)=FSRNFG(3)   !  ocean ice
      FSF(I,J,3)=FSRNFG(4)   !  land ice
      FSF(I,J,4)=FSRNFG(2)   !  soil
      IF(I.EQ.IWRITE.AND.J.EQ.JWRITE) CALL WRITER(6,ITWRITE)
      SRHR(I,J,1)=SRNFLB(1)
      TRHR(I,J,1)=STBO*(POCEAN*TGO**4+POICE*TGOI**4+PLICE*TGLI**4
     *  +PEARTH*TGE**4)-TRNFLB(1)
      DO 440 L=1,LM
      SRHR(I,J,L+1)=SRFHRL(L)
  440 TRHR(I,J,L+1)=-TRFCRL(L)
      DO 450 LR=1,LM_REQ
      SRHRS(I,J,LR)=SRFHRL(LM+LR)
  450 TRHRS(I,J,LR)=-TRFCRL(LM+LR)
      DO 460 K=1,4
      SNFS(I,J,K)=SRNFLB(K+LM)
  460 TNFS(I,J,K)=TRNFLB(K+LM)-TRNFLB(1)
         TRINCG(I,J)=TRDFLB(1)
         BTMPW(I,J)=BTEMPW-TF
         ALB(I,J,1)=SRNFLB(1)/(SRDFLB(1)+1.D-20)
         ALB(I,J,2)=PLAVIS
         ALB(I,J,3)=PLANIR
         ALB(I,J,4)=ALBVIS
         ALB(I,J,5)=ALBNIR
         ALB(I,J,6)=SRRVIS
         ALB(I,J,7)=SRRNIR
         ALB(I,J,8)=SRAVIS
         ALB(I,J,9)=SRANIR
  500 IM1=I
C****
C**** END OF MAIN LOOP FOR I INDEX
C****
  600 CONTINUE
C****
C**** END OF MAIN LOOP FOR J INDEX
C****
C****
C**** ACCUMULATE THE RADIATION DIAGNOSTICS
C****
         DO 780 J=1,JM
         DXYPJ=DXYP(J)
         IMAX=IMAXJ(J)
         DO 720 L=1,LM
         ASRHR=0.
         ATRHR=0.
         DO 710 I=1,IMAX
         ASRHR=ASRHR+SRHR(I,J,L+1)*COSZ2(I,J)
  710    ATRHR=ATRHR+TRHR(I,J,L+1)
         AJL(J,L,9)=AJL(J,L,9)+ASRHR
  720    AJL(J,L,10)=AJL(J,L,10)+ATRHR
         ASNFS1=0.
         BSNFS1=0.
         CSNFS1=0.
         ATNFS1=0.
         BTNFS1=0.
         CTNFS1=0.
         DO 770 I=1,IMAX
         COSZ=COSZ2(I,J)
         PLAND=FLAND(I,J)
         POICE=ODATA(I,J,2)*(1.-PLAND)
         POCEAN=(1.-PLAND)-POICE
         JR=JREG(I,J)
         DO 740 LR=1,LM_REQ
         ASJL(J,LR,3)=ASJL(J,LR,3)+SRHRS(I,J,LR)*COSZ
  740    ASJL(J,LR,4)=ASJL(J,LR,4)+TRHRS(I,J,LR)
         DO KR=1,NDLYPT
            IF (I.EQ.IJD6(1,KR).AND.J.EQ.IJD6(2,KR)) THEN
               IH=IHOUR
               DO INCH=1,INCHM
                  IF (IH.GT.24) IH=IH-24
                  ADAILY(IH,2,KR)=ADAILY(IH,2,KR)+(1.-SNFS(I,J,4)/S0)
                  ADAILY(IH,3,KR)=ADAILY(IH,3,KR)+(1.-ALB(I,J,1))
                  ADAILY(IH,4,KR)=ADAILY(IH,4,KR)
     *                 +((SNFS(I,J,4)-SNFS(I,J,1))*COSZ-TNFS(I,J,4)
     *                 +TNFS(I,J,1))
                  IH=IH+1
               END DO
            END IF
         END DO
  750    CONTINUE
         AJ(J,1)=AJ(J,1)+(S0*COSZ)*POCEAN
         BJ(J,1)=BJ(J,1)+(S0*COSZ)*PLAND
         CJ(J,1)=CJ(J,1)+(S0*COSZ)*POICE
         AREG(JR,1)=AREG(JR,1)+(S0*COSZ)*DXYPJ
         AJ(J,2)=AJ(J,2)+(SNFS(I,J,4)*COSZ)*POCEAN
         BJ(J,2)=BJ(J,2)+(SNFS(I,J,4)*COSZ)*PLAND
         CJ(J,2)=CJ(J,2)+(SNFS(I,J,4)*COSZ)*POICE
         AREG(JR,2)=AREG(JR,2)+(SNFS(I,J,4)*COSZ)*DXYPJ
         ASNFS1=ASNFS1+(SNFS(I,J,1)*COSZ)*POCEAN
         BSNFS1=BSNFS1+(SNFS(I,J,1)*COSZ)*PLAND
         CSNFS1=CSNFS1+(SNFS(I,J,1)*COSZ)*POICE
         AREG(JR,3)=AREG(JR,3)+(SNFS(I,J,1)*COSZ)*DXYPJ
         AJ(J,5)=AJ(J,5)+(SRHR(I,J,1)*COSZ/(ALB(I,J,1)+1.D-20))*POCEAN
         BJ(J,5)=BJ(J,5)+(SRHR(I,J,1)*COSZ/(ALB(I,J,1)+1.D-20))*PLAND
         CJ(J,5)=CJ(J,5)+(SRHR(I,J,1)*COSZ/(ALB(I,J,1)+1.D-20))*POICE
         AREG(JR,5)=AREG(JR,5)+
     *     (SRHR(I,J,1)*COSZ/(ALB(I,J,1)+1.D-20))*DXYPJ
         AJ(J,6)=AJ(J,6)+(FSF(I,J,1)*COSZ)*POCEAN
         SRNFLG=FSF(I,J,3)*FLICE(I,J)+FSF(I,J,4)*(PLAND-FLICE(I,J))
         BJ(J,6)=BJ(J,6)+(SRNFLG*COSZ)
         CJ(J,6)=CJ(J,6)+(FSF(I,J,2)*COSZ)*POICE
         AREG(JR,6)=AREG(JR,6)+(SRHR(I,J,1)*COSZ)*DXYPJ
         AJ(J,55)=AJ(J,55)+BTMPW(I,J)*POCEAN
         BJ(J,55)=BJ(J,55)+BTMPW(I,J)*PLAND
         CJ(J,55)=CJ(J,55)+BTMPW(I,J)*POICE
         AREG(JR,55)=AREG(JR,55)+BTMPW(I,J)*DXYPJ
         AJ(J,67)=AJ(J,67)+TRINCG(I,J)*POCEAN
         BJ(J,67)=BJ(J,67)+TRINCG(I,J)*PLAND
         CJ(J,67)=CJ(J,67)+TRINCG(I,J)*POICE
         AREG(JR,67)=AREG(JR,67)+TRINCG(I,J)*DXYPJ
         AJ(J,70)=AJ(J,70)-TNFS(I,J,4)*POCEAN
         BJ(J,70)=BJ(J,70)-TNFS(I,J,4)*PLAND
         CJ(J,70)=CJ(J,70)-TNFS(I,J,4)*POICE
         AREG(JR,70)=AREG(JR,70)-TNFS(I,J,4)*DXYPJ
         ATNFS1=ATNFS1-TNFS(I,J,1)*POCEAN
         BTNFS1=BTNFS1-TNFS(I,J,1)*PLAND
         CTNFS1=CTNFS1-TNFS(I,J,1)*POICE
         AREG(JR,71)=AREG(JR,71)-TNFS(I,J,1)*DXYPJ
         DO 760 K=2,9
         AJ(J,K+70)=AJ(J,K+70)+(S0*COSZ)*ALB(I,J,K)*POCEAN
         BJ(J,K+70)=BJ(J,K+70)+(S0*COSZ)*ALB(I,J,K)*PLAND
         CJ(J,K+70)=CJ(J,K+70)+(S0*COSZ)*ALB(I,J,K)*POICE
  760    AREG(JR,K+70)=AREG(JR,K+70)+(S0*COSZ)*ALB(I,J,K)*DXYPJ
         AIJ(I,J,IJ_TRNFP0)=AIJ(I,J,IJ_TRNFP0)-TNFS(I,J,4)
         AIJ(I,J,IJ_SRNFP0)=AIJ(I,J,IJ_SRNFP0)+(SNFS(I,J,4)*COSZ)
         AIJ(I,J,IJ_SRINCP0)=AIJ(I,J,IJ_SRINCP0)+(S0*COSZ)
         AIJ(I,J,IJ_SRNFG)=AIJ(I,J,IJ_SRNFG)+(SRHR(I,J,1)*COSZ)
         AIJ(I,J,IJ_SRINCG)=AIJ(I,J,IJ_SRINCG)+(SRHR(I,J,1)*COSZ/
     *        (ALB(I,J,1)+1.D-20))
         AIJ(I,J,IJ_BTMPW)=AIJ(I,J,IJ_BTMPW)+BTMPW(I,J)
         AIJ(I,J,IJ_SRREF)=AIJ(I,J,IJ_SRREF)+S0*COSZ*ALB(I,J,2)
  770    CONTINUE
         AJ(J,3)=AJ(J,3)+ASNFS1
         BJ(J,3)=BJ(J,3)+BSNFS1
         CJ(J,3)=CJ(J,3)+CSNFS1
         AJ(J,71)=AJ(J,71)+ATNFS1
         BJ(J,71)=BJ(J,71)+BTNFS1
         CJ(J,71)=CJ(J,71)+CTNFS1
  780    CONTINUE
         DO 790 L=1,LM
         DO 790 I=1,IM
         AIL(I,L,7)=AIL(I,L,7)+((SRHR(I,JEQ-2,L+1)*COSZ2(I,JEQ-2)+
     *     TRHR(I,JEQ-2,L+1))*DXYP(JEQ-2)+(SRHR(I,JEQ-1,L+1)*
     *     COSZ2(I,JEQ-1)+TRHR(I,JEQ-1,L+1))*DXYP(JEQ-1)+
     *     (SRHR(I,JEQ,L+1)*COSZ2(I,JEQ)+TRHR(I,JEQ,L+1))*DXYP(JEQ))
         AIL(I,L,11)=AIL(I,L,11)+(SRHR(I,J50N,L+1)*COSZ2(I,J50N)+
     *     TRHR(I,J50N,L+1))*DXYP(J50N)
  790    AIL(I,L,15)=AIL(I,L,15)+(SRHR(I,J70N,L+1)*COSZ2(I,J70N)+
     *     TRHR(I,J70N,L+1))*DXYP(J70N)
C****
C**** Update radiative equilibrium temperatures
C****
      DO LR=1,LM_REQ
         DO J=1,JM
            IMAX=IMAXJ(J)
            DO I=1,IMAX
               RQT(I,J,LR)=RQT(I,J,LR)+(SRHRS(I,J,LR)*COSZ2(I,J)
     *              +TRHRS(I,J,LR))*COE(LR+LM)
            END DO
         END DO
      END DO
C****
C**** Update other temperatures every physics time step
C****
  900 DO L=1,LS1-1
         DO J=1,JM
            IMAX=IMAXJ(J)
            DO I=1,IMAX
               T(I,J,L)=T(I,J,L)+(SRHR(I,J,L+1)*COSZ1(I,J)+TRHR(I,J,L+1)
     *              )*COE(L)/(P(I,J)*PK(L,I,J))
            END DO
         END DO
      END DO
      DO L=LS1,LM
         DO J=1,JM
            IMAX=IMAXJ(J)
            DO I=1,IMAX
               T(I,J,L)=T(I,J,L)+(SRHR(I,J,L+1)*COSZ1(I,J)+TRHR(I,J,L+1)
     *              )*COE(L)/((PSF-PTOP)*PK(L,I,J))
            END DO
         END DO
      END DO
C**** daily diagnostics
      DO KR=1,NDLYPT
         ADAILY(IHOUR,1,KR)=ADAILY(IHOUR,1,KR)+S0*COSZ1(IJD6(1,KR)
     *        ,IJD6(2,KR))
      END DO

      RETURN
      END
      SUBROUTINE GROUND
C****
C**** THIS SUBROUTINE USES THE SURFACE FLUXES TO PREDICT IN TIME THE
C**** GROUND TEMPERATURE, GROUND WATER AND ICE, AND SNOW MELTING.
C****
      USE CONSTANT, only : grav,rgas,kapa,sday,lhm,lhe,lhs,twopi,omega
     *     ,rhow,rhoi,shv,shw,shi
      USE E001M12_COM
      USE GEOM
      USE PBLCOM, only : tsavg
      USE GHYCOM, only : ghdata
      USE DAGCOM  !, only : aj,bj,cj,areg,aij,jreg
      USE OCEAN, only : ODATA,XSI1,XSI2,XSI3,XSI4,Z1I
     *     ,Z1O,Z2OIM,ACE1I,AC2OIM,OTA,OTB,OTC,T50, SEA_ICE 
      IMPLICIT NONE

      REAL*8, PARAMETER :: ALPHA = 1.0, dSNdML = 0.,FLEAD = 0.10d0
      REAL*8 MELT1, MELT4, MSI1, MSI2
      REAL*8, DIMENSION(IM,JM,4) :: E0,E1,EVAPOR
      COMMON/WORK3/E0,E1,EVAPOR
      REAL*8, DIMENSION(IM,JM,3) :: GDEEP
      COMMON/oldDAG/GDEEP

      REAL*8, PARAMETER :: ALAMI=2.1762 ,Z2LI=2.9, Z1E=.1, Z2E=4.,
     *     Z2OIX=4.9

      INTEGER I,J,L,KR,JR,IMAX,K
      REAL*8 FACT_T50,FACT_TSAVG,DTSRCE,ATRUNC,BYZICX,HC1I,HC2LI,HC1DE
     *     ,HC2DE,DIFFUS,ANGLE,SINANG,SN2ANG,SN3ANG,SN4ANG,COSANG,CS2ANG
     *     ,CS3ANG,CS4ANG,BF1DT,CF1DT,AOTDT,COTDT,AEFO,CEFI,BEDIFS
     *     ,CEDIFS,BERUN0,CF2DT,BERUN2,CERUN2,AERUN4,CERUN4,ATG1,BTG1
     *     ,CTG1,ATG2,BTG2,CTG2,ATG3,AEVAP,BEVAP,CEVAP,BDIFS,CDIFS,AIFO
     *     ,CIFI,BRUN0,CRUN0,BRUN2,CRUN2,ARUN4,CRUN4,BWTR1,BACE1,BWTR2
     *     ,BACE2,CACE2,BSNOW,CSNOW,CICOV,PLAND,PWATER,PLICE,PEARTH
     *     ,ROICE,POICE,POCEAN,DXYPJ,ACE1S,ACE2S,EVAPS,RUN0S,DIFSS,EVAP
     *     ,F0DT,OTDT,RUN4,ERUN4,ENRGO,EDIFSI,DIFSI,F1DT,AIFI,SNOW
     *     ,SNANDI,HC1,ENRG1,ACE1,ACE2,YSI1,YSI2,YSI3,YSI4,SNOWS,WTR1S
     *     ,WTR2S,TG1S,TG2S,TGW,WTRO,TG1,TG2,WTR1,WTR2,RUN0,RUN4,ERUN4
     *     ,DIFSI,EDIFSI,DIFS,EDIFS,ENRGFO,ACEFO,ACE2M,ACE2F,ENRGFI,F2DT
C****
C**** FLAND     LAND COVERAGE (1)
C**** FLICE     LAND ICE COVERAGE (1)
C****
C**** ODATA  1  OCEAN TEMPERATURE (C)
C****        2  RATIO OF OCEAN ICE COVERAGE TO WATER COVERAGE (1)
C****        3  OCEAN ICE AMOUNT OF SECOND LAYER (KG/M**2)
C****
C**** GDATA  1  OCEAN ICE SNOW AMOUNT (KG/M**2)
C****        2  EARTH SNOW AMOUNT (KG/M**2)
C****        3  OCEAN ICE TEMPERATURE OF FIRST LAYER (C)
C****        4  EARTH TEMPERATURE OF FIRST LAYER (C)
C****        5  EARTH WATER OF FIRST LAYER (KG/M**2)
C****        6  EARTH ICE OF FIRST LAYER (KG/M**2)
C****        7  OCEAN ICE TEMPERATURE OF SECOND LAYER (C)
C****        8  currently not used
C****       12  LAND ICE SNOW AMOUNT (KG/M**2)
C****       13  LAND ICE TEMPERATURE OF FIRST LAYER (C)
C****       14  LAND ICE TEMPERATURE OF SECOND LAYER (C)
C****       15  OCEAN ICE TEMPERATURE OF THIRD LAYER (C)
C****       16  OCEAN ICE TEMPERATURE OF FOURTH LAYER (C)
C****
C**** VDATA  9  WATER FIELD CAPACITY OF FIRST LAYER (KG/M**2)
C****       10  WATER FIELD CAPACITY OF SECOND LAYER (KG/M**2)
C****
C*
C***  Make 50 day mean of surface air temperature
C***    for lake ice extent calculation
C*
      FACT_T50 = 1.-1./(24.*50.)
      FACT_TSAVG = 1./(24.*50.)
      DO J = 1,JM
        DO I = 1,IM
          T50(I,J) = T50(I,J)*FACT_T50
     *             + (TSAVG(I,J)-273.16)*FACT_TSAVG
        END DO
      END DO
C*
   10 DTSRCE=NDYN*DT
c      ACE1I=Z1I*RHOI
c      AC2OIM=Z2OIM*RHOI
C*
      YSI1 = XSI1*ACE1I/(ACE1I+AC2OIM)
      YSI2 = XSI2*ACE1I/(ACE1I+AC2OIM)
      YSI3 = XSI3*AC2OIM/(ACE1I+AC2OIM)
      YSI4 = XSI4*AC2OIM/(ACE1I+AC2OIM)
C*
      ATRUNC=0.
      BYZICX=1./(Z1I+Z2OIX)
      HC1I=ACE1I*SHI
      HC2LI=Z2LI*RHOI*SHI
      HC1DE=Z1E*1129950.
      HC2DE=Z2E*1129950.+3.5*.125*RHOW*3100.
      DIFFUS=DTSRCE/SDAY
   50 ANGLE=TWOPI*JDAY/365.
      SINANG=SIN(ANGLE)
      SN2ANG=SIN(2*ANGLE)
      SN3ANG=SIN(3*ANGLE)
      SN4ANG=SIN(4*ANGLE)
      COSANG=COS(ANGLE)
      CS2ANG=COS(2*ANGLE)
      CS3ANG=COS(3*ANGLE)
      CS4ANG=COS(4*ANGLE)
C****
C**** OUTSIDE LOOP OVER J AND I, EXECUTED ONCE FOR EACH GRID POINT
C****
      DO 980 J=1,JM
      IMAX=IMAXJ(J)
         BF1DT=0.
         CF1DT=0.
         AOTDT=0.
         COTDT=0.
         AEFO=0.
         CEFI=0.
         BEDIFS=0.
         CEDIFS=0.
         BERUN0=0.
         CF2DT=0.
         BERUN2=0.
         CERUN2=0.
         AERUN4=0.
         CERUN4=0.
         ATG1=0.
         BTG1=0.
         CTG1=0.
         ATG2=0.
         BTG2=0.
         CTG2=0.
         ATG3=0.
         AEVAP=0.
         BEVAP=0.
         CEVAP=0.
         BDIFS=0.
         CDIFS=0.
         AIFO=0.
         CIFI=0.
         BRUN0=0.
         CRUN0=0.
         BRUN2=0.
         CRUN2=0.
         ARUN4=0.
         CRUN4=0.
         BWTR1=0.
         BACE1=0.
         BWTR2=0.
         BACE2=0.
         CACE2=0.
         BSNOW=0.
         CSNOW=0.
         CICOV=0.
      DO 960 I=1,IMAX
C****
C**** DETERMINE SURFACE CONDITIONS
C****
      PLAND=FLAND(I,J)
      PWATER=1.-PLAND
      PLICE=FLICE(I,J)
      PEARTH=FEARTH(I,J)
      ROICE=ODATA(I,J,2)
      POICE=ROICE*PWATER
      POCEAN=PWATER-POICE
         JR=JREG(I,J)
         DXYPJ=DXYP(J)
         SNOWS=0.
         WTR1S=0.
         ACE1S=0.
         WTR2S=0.
         ACE2S=0.
         TG1S=0.
         TG2S=0.
         EVAPS=0.
         RUN0S=0.
         DIFSS=0.
C****
      IF (PWATER.LE.0.) GO TO 400
C****
C**** OCEAN
C****
      EVAP=EVAPOR(I,J,1)
         ATG1=ATG1+ODATA(I,J,1)*POCEAN
         TG1S=TG1S+ODATA(I,J,1)*POCEAN
         AEVAP=AEVAP+EVAP*POCEAN
         EVAPS=EVAPS+EVAP*POCEAN
         AIJ(I,J,IJ_TGO)=AIJ(I,J,IJ_TGO)+ODATA(I,J,1)
         AIJ(I,J,IJ_EVAPO)=AIJ(I,J,IJ_EVAPO)+EVAP*POCEAN
      IF (KOCEAN .NE. 1) THEN 
         ATG2=ATG2+ODATA(I,J,1)*POCEAN
         TG2S=TG2S+ODATA(I,J,1)*POCEAN
      ELSE
      TGW=ODATA(I,J,1)
      WTRO=Z1O(I,J)*RHOW
      F0DT=E0(I,J,1)
         AIJ(I,J,IJ_F0OC)=AIJ(I,J,IJ_F0OC)+F0DT*POCEAN
      OTDT=DTSRCE*(OTA(I,J,4)*SN4ANG+OTB(I,J,4)*CS4ANG
     *            +OTA(I,J,3)*SN3ANG+OTB(I,J,3)*CS3ANG
     *            +OTA(I,J,2)*SN2ANG+OTB(I,J,2)*CS2ANG
     *            +OTA(I,J,1)*SINANG+OTB(I,J,1)*COSANG+OTC(I,J))
         ATG2=ATG2+ODATA(I,J,4)*POCEAN
         TG2S=TG2S+ODATA(I,J,4)*POCEAN
         ATG3=ATG3+ODATA(I,J,5)*POCEAN
         AOTDT=AOTDT+OTDT*POCEAN
      RUN4=-EVAP
      ERUN4=RUN4*TGW*SHW
         AERUN4=AERUN4+ERUN4*POCEAN
         ARUN4=ARUN4+RUN4*POCEAN
      ENRGO=F0DT+OTDT-ERUN4
      END IF
C* 
C***  CALL SEA ICE SUBROUTINE 
C* 
      EDIFSI = 0.0 
      DIFSI  = 0.0 
C* 
      F0DT=E0(I,J,2) ! heat flux to the top ice surface (J/m^2) 
      F1DT=E1(I,J,2) ! heat flux between 1st and 2nd ice layers (J/m^2)
      EVAP=EVAPOR(I,J,2) ! evaporation/dew at the ice surface (kg/m^2) 
      MSI2=ODATA(I,J,3) 
      CALL SEA_ICE(DTSRCE,I,J,POICE,F0DT,F1DT,EVAP,
     *             TGW,WTRO,OTDT,ENRGO,
     *             RUN0,RUN4,ERUN4,DIFSI,EDIFSI,DIFS,EDIFS, 
     *             ENRGFO,ACEFO,ACE2M,ACE2F,ENRGFI,F2DT)
C****
         AIJ(I,J,IJ_RSOI) =AIJ(I,J,IJ_RSOI) +POICE
         AIJ(I,J,IJ_MSI2) =AIJ(I,J,IJ_MSI2) +MSI2*POICE
         AIJ(I,J,IJ_F0OI) =AIJ(I,J,IJ_F0OI) +F0DT*POICE
         AIJ(I,J,IJ_EVAPI)=AIJ(I,J,IJ_EVAPI)+EVAP*POICE
      IF (KOCEAN .EQ. 1) THEN 
         AEFO=AEFO-ENRGFO*POCEAN
         AIFO=AIFO-ACEFO*POCEAN
C*
         CERUN4=CERUN4+ERUN4*POICE
         CRUN4=CRUN4+RUN4*POICE
C*
         AIFI=AIFI+ACE2M*POICE
C*
         CEFI=CEFI-ENRGFI*POICE
         CIFI=CIFI-ACE2F*POICE
C*
         COTDT=COTDT+OTDT*POICE
         CEDIFS=CEDIFS+EDIFSI*PWATER
         CDIFS=CDIFS+DIFSI*PWATER
         DIFSS=DIFSS+DIFSI*PWATER
C*
      ELSE 
         CEDIFS=CEDIFS+EDIFS*POICE
         CDIFS=CDIFS+DIFS*POICE
         CERUN2=CERUN2+EDIFS*POICE
         CRUN2=CRUN2+DIFS*POICE
         DIFSS=DIFSS+DIFS*POICE

      END IF 
         CRUN0=CRUN0+RUN0*POICE
         RUN0S=RUN0S+RUN0*POICE
C*
      MSI2 = ODATA(I,J,3)
      SNOW = GDATA(I,J,1) 
      TG1 = GDATA(I,J,3) 
      TG2 = GDATA(I,J,7) 
         CSNOW=CSNOW+SNOW*POICE
         CTG1=CTG1+TG1*POICE
         CTG2=CTG2+TG2*POICE
         CACE2=CACE2+MSI2*POICE
         CF1DT=CF1DT+F1DT*POICE
         CF2DT=CF2DT+F2DT*POICE
         CEVAP=CEVAP+EVAP*POICE
         CICOV=CICOV+POICE
         SNOWS=SNOWS+SNOW*POICE
         TG1S=TG1S+TG1*POICE
         ACE1S=ACE1S+ACE1I*POICE
         ACE2S=ACE2S+MSI2*POICE
         TG2S=TG2S+TG2*POICE
         EVAPS=EVAPS+EVAP*POICE
C****
  400 IF (PLICE.LE.0.) GO TO 600
C****
C**** LAND ICE
C****
      SNOW=GDATA(I,J,12)
      TG1=GDATA(I,J,13)
      TG2=GDATA(I,J,14)
      F0DT=E0(I,J,3)
         AIJ(I,J,IJ_F0LI)=AIJ(I,J,IJ_F0LI)+F0DT
      F1DT=E1(I,J,3)
      EVAP=EVAPOR(I,J,3)
         AIJ(I,J,IJ_EVAPLI)=AIJ(I,J,IJ_EVAPLI)+EVAP
C**** CALCULATE TG1
      SNANDI=SNOW+ACE1I-EVAP
      HC1=SNANDI*SHI
      ENRG1=F0DT+EVAP*(TG1*SHI-LHM)-F1DT
      IF (ENRG1.LE.-TG1*HC1) GO TO 420
C**** FLUXES HEAT UP TG1 TO FREEZING POINT AND MELT SOME SNOW AND ICE
      RUN0=(ENRG1+TG1*HC1)/LHM
      TG1=0.
      SNANDI=SNANDI-RUN0
         BRUN0=BRUN0+RUN0*PLICE
         RUN0S=RUN0S+RUN0*PLICE
         AIJ(I,J,IJ_RUNLI)=AIJ(I,J,IJ_RUNLI)+RUN0
      GO TO 440
C**** FLUXES RECOMPUTE TG1 WHICH IS BELOW FREEZING POINT
  420 TG1=TG1+ENRG1/HC1
  440 IF (SNANDI.GE.ACE1I) GO TO 460
C**** SOME ICE HAS MELTED OR EVAPORATED, TAKE IT FROM G2
      SNOW=0.
      DIFS=SNANDI-ACE1I
      TG1=(TG1*SNANDI-TG2*DIFS)/ACE1I
      EDIFS=DIFS*(TG2*SHI-LHM)
         BEDIFS=BEDIFS+EDIFS*PLICE
         AIJ(I,J,IJ_F1LI)=AIJ(I,J,IJ_F1LI)+EDIFS
         BDIFS=BDIFS+DIFS*PLICE
         DIFSS=DIFSS+DIFS*PLICE
         BERUN2=BERUN2+EDIFS*PLICE
         AIJ(I,J,IJ_ERUN2)=AIJ(I,J,IJ_ERUN2)+EDIFS
         BRUN2=BRUN2+DIFS*PLICE
      GO TO 500
  460 SNOW=SNANDI-ACE1I
C**** CALCULATE TG2
  500 TG2=TG2+F1DT/HC2LI
C**** RESAVE PROGNOSTIC QUANTITIES
      GDATA(I,J,12)=SNOW
      GDATA(I,J,13)=TG1
      GDATA(I,J,14)=TG2
         BSNOW=BSNOW+SNOW*PLICE
         BTG1=BTG1+TG1*PLICE
         BTG2=BTG2+TG2*PLICE
         BF1DT=BF1DT+F1DT*PLICE
         AIJ(I,J,IJ_F1LI)=AIJ(I,J,IJ_F1LI)+F1DT
         BEVAP=BEVAP+EVAP*PLICE
         SNOWS=SNOWS+SNOW*PLICE
         TG1S=TG1S+TG1*PLICE
         ACE1S=ACE1S+ACE1I*PLICE
         ACE2S=ACE2S+Z2LI*RHOI*PLICE
         TG2S=TG2S+TG2*PLICE
         EVAPS=EVAPS+EVAP*PLICE
C****
  600 IF (PEARTH.LE.0.) GO TO 940
C****
C**** EARTH (mostly done in subroutine EARTH, called from SURFCE)
C****
         SNOW=GDATA(I,J,2)
         TG1=GDATA(I,J,4)
         WTR1=GDATA(I,J,5)
         ACE1=GDATA(I,J,6)
         TG2=GDEEP(I,J,1)
         WTR2=GDEEP(I,J,2)
         ACE2=GDEEP(I,J,3)
         F0DT=E0(I,J,4)
         AIJ(I,J,IJ_F0E)=AIJ(I,J,IJ_F0E)+F0DT
         F1DT=E1(I,J,4)
         EVAP=EVAPOR(I,J,4)
         EVAPS=EVAPS+EVAP*PEARTH
         AIJ(I,J,IJ_EVAPE)=AIJ(I,J,IJ_EVAPE)+EVAP
         BSNOW=BSNOW+SNOW*PEARTH
         BTG1=BTG1+TG1*PEARTH
         BTG2=BTG2+TG2*PEARTH
         BWTR1=BWTR1+WTR1*PEARTH
         BACE1=BACE1+ACE1*PEARTH
         BWTR2=BWTR2+WTR2*PEARTH
         BACE2=BACE2+ACE2*PEARTH
         BF1DT=BF1DT+F1DT*PEARTH
         BEVAP=BEVAP+EVAP*PEARTH
         SNOWS=SNOWS+SNOW*PEARTH
         TG1S=TG1S+TG1*PEARTH
         WTR1S=WTR1S+WTR1*PEARTH
         ACE1S=ACE1S+ACE1*PEARTH
         WTR2S=WTR2S+WTR2*PEARTH
         ACE2S=ACE2S+ACE2*PEARTH
         TG2S=TG2S+TG2*PEARTH
C        AIJ(I,J,IJ_SSAT)=AIJ(I,J,IJ_SSAT)+(WTR1+ACE1)/WFC1
         AIJ(I,J,IJ_GWTR)=AIJ(I,J,IJ_GWTR)+(WTR1+ACE1+WTR2+ACE2)
C****
C**** ACCUMULATE DIAGNOSTICS
C****
C**** QUANTITIES ACCUMULATED FOR REGIONS IN DIAGJ
  940    IF (JR.EQ.24) GO TO 950
         AREG(JR,17)=AREG(JR,17)+TG2S*DXYPJ
         AREG(JR,18)=AREG(JR,18)+TG1S*DXYPJ
         AREG(JR,30)=AREG(JR,30)+POICE*DXYPJ
         AREG(JR,45)=AREG(JR,45)+DIFSS*DXYPJ ! ocn/land ice contribution
         AREG(JR,49)=AREG(JR,49)+WTR1S*DXYPJ
         AREG(JR,50)=AREG(JR,50)+ACE1S*DXYPJ
         AREG(JR,51)=AREG(JR,51)+WTR2S*DXYPJ
         AREG(JR,52)=AREG(JR,52)+ACE2S*DXYPJ
         AREG(JR,53)=AREG(JR,53)+SNOWS*DXYPJ
         AREG(JR,54)=AREG(JR,54)+RUN0S*DXYPJ
C**** QUANTITIES ACCUMULATED FOR LATITUDE-LONGITUDE MAPS IN DIAGIJ
  950    AIJ(I,J,IJ_EVAP)=AIJ(I,J,IJ_EVAP)+EVAPS
         AIJ(I,J,IJ_TG1) =AIJ(I,J,IJ_TG1)+TG1S
         DO K=1,4
            AIJG(I,J,K)=AIJG(I,J,K)+GHDATA(I,J,K)
         END DO
         DO K=7,10
            AIJG(I,J,K)=AIJG(I,J,K)+GHDATA(I,J,K)
         END DO
         AIJG(I,J,28)=AIJG(I,J,28)+GHDATA(I,J,28)
         AIJG(I,J,29)=AIJG(I,J,29)+GHDATA(I,J,29)
  960 CONTINUE
C**** LONGITUDINALLY INTEGRATED QUANTITIES FOR DIAGJ
         CJ(J,15)=CJ(J,15)+CF2DT
         AJ(J,17)=AJ(J,17)+ATG2
         BJ(J,17)=BJ(J,17)+BTG2
         CJ(J,17)=CJ(J,17)+CTG2
         AJ(J,18)=AJ(J,18)+ATG1
         BJ(J,18)=BJ(J,18)+BTG1
         CJ(J,18)=CJ(J,18)+CTG1
         AJ(J,19)=AJ(J,19)+AEVAP
         BJ(J,19)=BJ(J,19)+BEVAP
         CJ(J,19)=CJ(J,19)+CEVAP
         CJ(J,30)=CJ(J,30)+CICOV
         AJ(J,33)=AJ(J,33)+AOTDT
         CJ(J,33)=CJ(J,33)+COTDT
         AJ(J,34)=AJ(J,34)+ATG3
C        BJ(J,40)=BJ(J,40)+BERUN0 ! land ice (Tg=0)
         BJ(J,41)=BJ(J,41)+BEDIFS
         CJ(J,41)=CJ(J,41)+CEDIFS
         BJ(J,42)=BJ(J,42)+BF1DT
         CJ(J,42)=CJ(J,42)+CF1DT
         AJ(J,43)=AJ(J,43)+AEFO
         BJ(J,43)=BJ(J,43)+BERUN2
         CJ(J,43)=CJ(J,43)+(CERUN2+CEFI)
         BJ(J,45)=BJ(J,45)+BDIFS
         CJ(J,45)=CJ(J,45)+CDIFS
         AJ(J,46)=AJ(J,46)+AIFO
         BJ(J,46)=BJ(J,46)+BRUN2
         CJ(J,46)=CJ(J,46)+(CRUN2+CIFI)
         AJ(J,47)=AJ(J,47)+ARUN4
         CJ(J,47)=CJ(J,47)+CRUN4
         AJ(J,48)=AJ(J,48)+AERUN4
         CJ(J,48)=CJ(J,48)+CERUN4
         BJ(J,49)=BJ(J,49)+BWTR1
         BJ(J,50)=BJ(J,50)+BACE1
         BJ(J,51)=BJ(J,51)+BWTR2
         BJ(J,52)=BJ(J,52)+BACE2
         CJ(J,52)=CJ(J,52)+CACE2
         BJ(J,53)=BJ(J,53)+BSNOW
         CJ(J,53)=CJ(J,53)+CSNOW
         BJ(J,54)=BJ(J,54)+BRUN0
         CJ(J,54)=CJ(J,54)+CRUN0
  980 CONTINUE
      RETURN
      END
      SUBROUTINE DRYCNV
C****
C**** THIS SUBROUTINE MIXES AIR CAUSED BY DRY CONVECTION.  SINCE DRY
C**** CONVECTION IN THE BOUNDARY LAYER IS DONE IN SUBROUTINE SURFCE,
C**** THIS ROUTINE ONLY CHECKS LAYERS 2 TO LM.
C****
      USE CONSTANT, only : grav,rgas,kapa,sday,lhm,lhe,lhs,twopi,omega
     *     ,sha
      USE E001M12_COM
      USE GEOM
      USE SOMTQ_COM
      USE DAGCOM !, only : ajl
      USE DYNAMICS, only : pk,PDSIG
      IMPLICIT NONE

      REAL*8, DIMENSION(IM,JM,LM) :: UT,VT
      REAL*8, DIMENSION(LM) :: DP
      COMMON/WORK2/UT,VT,DP
      INTEGER, DIMENSION(IM) :: IDI,IDJ    !@var ID
      REAL*8, DIMENSION(IM) :: RA !@var
      REAL*8, DIMENSION(IM) :: UMS,VMS !@var
      LOGICAL POLE
      INTEGER I,J,L,K,IMAX,KMAX,IM1,LMAX,LMIN

      REAL*8 RVX,DTSRCE,DOK,PIJBOT,PIJ,PKMS,THPKMS,QMS,TXS,TYS,TXXS,TYYS
     *     ,TXYS,QXS,QYS,QXXS,QYYS,QXYS,TVMS,THETA,RDP,THM

      RVX=0.
C**** LOAD U,V INTO UT,VT.  UT,VT WILL BE FIXED DURING DRY CONVECTION
C****   WHILE U,V WILL BE UPDATED.

      DTSRCE=DT*NDYN
      DO 50 L=1,LM
      DO 50 J=2,JM
      DO 50 I=1,IM
      UT(I,J,L)=U(I,J,L)
   50 VT(I,J,L)=V(I,J,L)
C**** OUTSIDE LOOPS OVER J AND I
      DO 500 J=1,JM
      POLE=.FALSE.
      IF (J.EQ.1.OR.J.EQ.JM) POLE=.TRUE.

      IMAX=IMAXJ(J)
      KMAX=KMAXJ(J)
C****
C**** MAIN LOOP
C****
      IM1=IM
      DO 500 I=1,IMAX
         DO K=1,KMAX
            RA(K)=RAJ(K,J)
            IDI(K)=IDIJ(K,I,J)
            IDJ(K)=IDJJ(K,J)
         END DO
      LMAX=1
  130 LMIN=LMAX+1
      IF (LMIN.GE.LM) GO TO 500
      LMAX=LMIN
      IF (T(I,J,LMIN)*(1.+Q(I,J,LMIN)*RVX).LE.
     *   T(I,J,LMIN+1)*(1.+Q(I,J,LMIN+1)*RVX)) GO TO 130
C**** MIX HEAT AND MOISTURE THROUGHOUT THE UNSTABLE LAYERS
C**** MIX THROUGH TWO LOWER LAYERS
      PIJBOT=P(I,J)
      IF(LMIN.GE.LS1) PIJBOT=PSF-PTOP
      DP(LMIN)=PIJBOT*DSIG(LMIN)
      PIJ=PIJBOT
      IF(LMIN+1.EQ.LS1) PIJ=PSF-PTOP
      DP(LMIN+1)=PIJ*DSIG(LMIN+1)
      PKMS=PK(LMIN,I,J)*DP(LMIN)+PK(LMIN+1,I,J)*DP(LMIN+1)
      THPKMS=T(I,J,LMIN)*(PK(LMIN,I,J)*DP(LMIN))
     *  +T(I,J,LMIN+1)*(PK(LMIN+1,I,J)*DP(LMIN+1))
      QMS=Q(I,J,LMIN)*DP(LMIN)+Q(I,J,LMIN+1)*DP(LMIN+1)
C**** sum moments to mix over unstable layers
      TXS=   TX(I,J,LMIN  )*(PK(LMIN  ,I,J)*DP(LMIN  ))  +
     *       TX(I,J,LMIN+1)*(PK(LMIN+1,I,J)*DP(LMIN+1))
      TYS=   TY(I,J,LMIN  )*(PK(LMIN  ,I,J)*DP(LMIN  ))  +
     *       TY(I,J,LMIN+1)*(PK(LMIN+1,I,J)*DP(LMIN+1))
      TXXS= TXX(I,J,LMIN  )*(PK(LMIN  ,I,J)*DP(LMIN  )) +
     *      TXX(I,J,LMIN+1)*(PK(LMIN+1,I,J)*DP(LMIN+1))
      TYYS= TYY(I,J,LMIN  )*(PK(LMIN  ,I,J)*DP(LMIN  )) +
     *      TYY(I,J,LMIN+1)*(PK(LMIN+1,I,J)*DP(LMIN+1))
      TXYS= TXY(I,J,LMIN  )*(PK(LMIN  ,I,J)*DP(LMIN  )) +
     *      TXY(I,J,LMIN+1)*(PK(LMIN+1,I,J)*DP(LMIN+1))
      QXS  =  QX(I,J,LMIN)*DP(LMIN) +  QX(I,J,LMIN+1)*DP(LMIN+1)
      QYS  =  QY(I,J,LMIN)*DP(LMIN) +  QY(I,J,LMIN+1)*DP(LMIN+1)
      QXXS = QXX(I,J,LMIN)*DP(LMIN) + QXX(I,J,LMIN+1)*DP(LMIN+1)
      QYYS = QYY(I,J,LMIN)*DP(LMIN) + QYY(I,J,LMIN+1)*DP(LMIN+1)
      QXYS = QXY(I,J,LMIN)*DP(LMIN) + QXY(I,J,LMIN+1)*DP(LMIN+1)
      IF (LMIN+1.GE.LM) GO TO 150
      TVMS=T(I,J,LMIN)*(1.+Q(I,J,LMIN)*RVX)*(PK(LMIN,I,J)*DP(LMIN))
     *    +T(I,J,LMIN+1)*(1.+Q(I,J,LMIN+1)*RVX)
     *                                  *(PK(LMIN+1,I,J)*DP(LMIN+1))
      THETA=TVMS/PKMS
C**** MIX THROUGH SUBSEQUENT UNSTABLE LAYERS
      DO 140 L=LMIN+2,LM
      IF (THETA.LT.T(I,J,L)*(1.+Q(I,J,L)*RVX)) GO TO 160
      IF(L.EQ.LS1) PIJ=PSF-PTOP
      DP(L)=PIJ*DSIG(L)
      PKMS=PKMS+(PK(L,I,J)*DP(L))
      THPKMS=THPKMS+T(I,J,L)*(PK(L,I,J)*DP(L))
      QMS=QMS+Q(I,J,L)*DP(L)
      TVMS=TVMS+T(I,J,L)*(1.+Q(I,J,L)*RVX)*(PK(L,I,J)*DP(L))
      TXS=   TXS +  TX(I,J,L)*(PK(L,I,J)*DP(L))
      TYS=   TYS +  TY(I,J,L)*(PK(L,I,J)*DP(L))
      TXXS= TXXS + TXX(I,J,L)*(PK(L,I,J)*DP(L))
      TYYS= TYYS + TYY(I,J,L)*(PK(L,I,J)*DP(L))
      TXYS= TXYS + TXY(I,J,L)*(PK(L,I,J)*DP(L))
      QXS  =  QXS +  QX(I,J,L)*DP(L)
      QYS  =  QYS +  QY(I,J,L)*DP(L)
      QXXS = QXXS + QXX(I,J,L)*DP(L)
      QYYS = QYYS + QYY(I,J,L)*DP(L)
      QXYS = QXYS + QXY(I,J,L)*DP(L)
  140 THETA=TVMS/PKMS
  150 L=LM+1
  160 LMAX=L-1
      RDP=1./(PIJBOT*SIGE(LMIN)-PIJ*SIGE(LMAX+1))
      THM=THPKMS/PKMS
      QMS=QMS*RDP
      PIJ=P(I,J)
      DO 180 L=LMIN,LMAX
      IF(L.GE.LS1) PIJ=PSF-PTOP
         AJL(J,L,12)=AJL(J,L,12)+(THM-T(I,J,L))*PK(L,I,J)*PIJ
         AJL(J,L,55)=AJL(J,L,55)+(QMS-Q(I,J,L))*PDSIG(L,I,J)*LHE/SHA
      T(I,J,L)=THM
       TX(I,J,L) = TXS/PKMS
       TY(I,J,L) = TYS/PKMS
       TZ(I,J,L) = 0.
      TXX(I,J,L) = TXXS/PKMS
      TYY(I,J,L) = TYYS/PKMS
      TXY(I,J,L) = TXYS/PKMS
      TZZ(I,J,L) = 0.
      TZX(I,J,L) = 0.
      TYZ(I,J,L) = 0.
      Q(I,J,L)=QMS
       QX(I,J,L) = QXS*RDP
       QY(I,J,L) = QYS*RDP
       QZ(I,J,L) = 0.
      QXX(I,J,L) = QXXS*RDP
      QYY(I,J,L) = QYYS*RDP
      QXY(I,J,L) = QXYS*RDP
      QZZ(I,J,L) = 0.
      QZX(I,J,L) = 0.
      QYZ(I,J,L) = 0.
  180 CONTINUE
C**** MIX MOMENTUM THROUGHOUT UNSTABLE LAYERS
      UMS(1:KMAX)=0.
      VMS(1:KMAX)=0.
      PIJ=P(I,J)
      DO L=LMIN,LMAX
         IF(L.EQ.LS1) PIJ=PSF-PTOP
         DO K=1,KMAX
            UMS(K)=UMS(K)+UT(IDI(K),IDJ(K),L)*DP(L)
            VMS(K)=VMS(K)+VT(IDI(K),IDJ(K),L)*DP(L)
         ENDDO
      ENDDO
      UMS(1:KMAX)=UMS(1:KMAX)*RDP
      VMS(1:KMAX)=VMS(1:KMAX)*RDP
      PIJ=P(I,J)
      DO L=LMIN,LMAX
         IF(L.EQ.LS1) PIJ=PSF-PTOP
         DO K=1,KMAX
            U(IDI(K),IDJ(K),L)=U(IDI(K),IDJ(K),L)
     &           +(UMS(K)-UT(IDI(K),IDJ(K),L))*RA(K)
            V(IDI(K),IDJ(K),L)=V(IDI(K),IDJ(K),L)
     &           +(VMS(K)-VT(IDI(K),IDJ(K),L))*RA(K)
c the following line gives bytewise different ajl
            AJL(IDJ(K),L,38)=AJL(IDJ(K),L,38)
     &           +(UMS(K)-UT(IDI(K),IDJ(K),L))*PIJ*RA(K)
         ENDDO
      ENDDO
      GO TO 130
  500 IM1=I
      RETURN
      END
      SUBROUTINE SDRAG
C****
C**** THIS SUBROUTINE PUTS A DRAG ON THE WINDS ON THE TOP LAYER OF
C**** THE ATMOSPHERE
C****
      USE CONSTANT, only : grav,rgas,kapa,sday,lhm,lhe,lhs,twopi,omega
      USE E001M12_COM
      USE GEOM
      USE DAGCOM, only : aij, IJ_WLM
      USE DYNAMICS, only : pk
      IMPLICIT NONE

      INTEGER I,J,IP1
      REAL*8 PIJU,WLM,RHO,CDN,X

      REAL*8 XCDLM(2)
      NAMELIST/SDRNML/XCDLM
      INTEGER :: IFIRST = 1
      IF(IFIRST.EQ.1) THEN
        READ(5,SDRNML)
        WRITE(6,SDRNML)
        IFIRST=0
      END IF
      DO 100 J=2,JM
      I=IM
      DO 100 IP1=1,IM
      PIJU=PSF-PTOP
      WLM=SQRT(U(I,J,LM)*U(I,J,LM)+V(I,J,LM)*V(I,J,LM))
      RHO=(PIJU*SIGE(LM+1)+PTOP)/(RGAS*T(I,J,LM)*PK(LM,I,J))
      CDN=XCDLM(1)+XCDLM(2)*WLM
         AIJ(I,J,IJ_WLM)=AIJ(I,J,IJ_WLM)+WLM
      X=NDYN*DT*RHO*CDN*WLM*GRAV/(PIJU*DSIG(LM))
      IF(X.GT.1) THEN
        write(99,*) 'SDRAG: TAU,I,J,PIJU,X,RHO,CDN,U(I,J,LM),V(I,J,LM)',
     *   TAU,I,J,PIJU,X,RHO,CDN,U(I,J,LM),V(I,J,LM)
        X=1.
      END IF
      U(I,J,LM)=U(I,J,LM)*(1.-X)
      V(I,J,LM)=V(I,J,LM)*(1.-X)
  100 I=IP1
      RETURN
      END

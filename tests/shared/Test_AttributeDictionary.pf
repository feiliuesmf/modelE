module Test_AttributeDictionary_mod
  use pfunit_mod
  use AttributeDictionary_mod
  use Attributes_mod

  implicit none

   public :: setUp
   public :: tearDown

   public :: Test_AttributeDictionary
   interface Test_AttributeDictionary
     module procedure newTest_AttributeDictionary
   end interface Test_AttributeDictionary

@TestCase
   type, extends(TestCase) :: Test_AttributeDictionary
    type (AttributeDictionary) :: dictionary
      procedure(runMethod), pointer :: userMethod => null()
   contains
      procedure :: setUp     ! overides generic
      procedure :: tearDown  ! overrides generic
      procedure :: runMethod ! runs userMethod
  end type Test_AttributeDictionary

contains

   function newTest_AttributeDictionary(name, userMethod) result(test)
     type (Test_AttributeDictionary) :: test
     character(len=*), intent(in) :: name
     procedure(runMethod) :: userMethod

     call test%setName(name)
     test%userMethod => userMethod

   end function newTest_AttributeDictionary

   subroutine runMethod(this)
     class (Test_AttributeDictionary), intent(inout) :: this
     call this%userMethod()
   end subroutine runMethod

  subroutine setUp(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    this%dictionary = newAttributeDictionary()
  end subroutine setUp

  subroutine tearDown(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    call clean(this%dictionary)
  end subroutine tearDown

  ! Test that class can insert intrinsic integer and cast to an integer pointer.
@Test  
  subroutine testInsertIntegerAttribute(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    integer, pointer :: pInteger

    call this%dictionary%insert('A', 1)

    pInteger = this%dictionary%getReference('A')
    @assertEqual(1, pInteger)

  end subroutine testInsertIntegerAttribute

@Test  
  subroutine testAssignAttributeReference(this)
    use AttributeReference_mod
    use IntegerAttribute_mod, only: assignment(=)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    type (AttributeReference) :: scalar
    integer, pointer :: i => null()

    type (IntegerAttribute) :: iAttr

    iAttr = newAttribute(1)
    
    call scalar%set(iattr)

    i = scalar
    @assertEqual(1, i)

  end subroutine testAssignAttributeReference

@Test  
  subroutine testAssignAttributeReferenceMulti(this)
    use AttributeReference_mod
    class (Test_AttributeDictionary), intent(inout) :: this
    
    type (AttributeReference), target :: vector(3)
    integer, pointer :: i(:)
    integer, pointer :: j

    type (IntegerAttribute) :: iAttr(3)

    iAttr(1) = newAttribute(1)
    iAttr(2) = newAttribute(2)
    iAttr(3) = newAttribute(5)

    call vector(1)%set(iAttr(1))
    call vector(2)%set(iAttr(2))
    call vector(3)%set(iAttr(3))

    i = vector

    @assertEqual([1,2,5], i)

  end subroutine testAssignAttributeReferenceMulti
  
  ! Test that class can insert/cast intrinsic 1D integer arrays
@Test  
  subroutine testInsertInteger1DAttribute(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    integer, pointer :: pInteger(:)
    call this%dictionary%insert('A', [1,2])

    pInteger = this%dictionary%getReference('A')
    @assertEqual([1,2], pInteger)

  end subroutine testInsertInteger1DAttribute

  ! Test that class can insert intrinsic double and cast to an double pointer.
@Test  
  subroutine testInsertRealDPAttribute(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    real*8, pointer :: pRealDP
    real*8, parameter :: VALUE = 1.23d0
    call this%dictionary%insert('A', VALUE)

    pRealDP = this%dictionary%getReference('A')
    @assertEqual(VALUE, pRealDP)

  end subroutine testInsertRealDPAttribute

  ! Test that class can insert/cast intrinsic 1D integer arrays
@Test  
  subroutine testInsertRealDP1DAttribute(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    real*8, pointer :: pRealDP(:)
    real*8, parameter :: VALUES(3) = [1.2,3.4,5.6]
    call this%dictionary%insert('A', VALUES)

    pRealDP = this%dictionary%getReference('A')
    @assertEqual(VALUES, pRealDP)

  end subroutine testInsertRealDP1DAttribute

  ! Test that class can insert intrinsic integer and cast to an integer pointer.
@Test  
  subroutine testInsertStringAttribute(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    character(len=MAX_LEN_ATTRIBUTE_STRING), pointer :: pString

    call this%dictionary%insert('A', 'foo')

    pString = this%dictionary%getReference('A')
    @assertEqual('foo', pString)

  end subroutine testInsertStringAttribute

  ! Test that class can insert intrinsic logical and cast to a logical pointer.
@Test  
  subroutine testInsertLogicalAttribute(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    
    logical, pointer :: pLogical
    logical, parameter :: TRUE = .true.
    logical, parameter :: FALSE = .false.

    call this%dictionary%insert('A', TRUE)
    call this%dictionary%insert('B', FALSE)

    pLogical = this%dictionary%getReference('A')
    @assertTrue(pLogical)
 
    pLogical = this%dictionary%getReference('B')
    @assertFalse(pLogical)

  end subroutine testInsertLogicalAttribute

@Test  
  subroutine testComplexCase(this)
    class (Test_AttributeDictionary), intent(inout) :: this
    integer, pointer :: betaValue
    real(kind=dp), pointer :: gammaValue
    logical, pointer :: deltaValues(:)

    call this%dictionary%insert('beta', 2)
    call this%dictionary%insert('gamma', 3.d0)
    call this%dictionary%insert('delta', [.true., .false., .true., .true.])

    @assertEqual(3, this%dictionary%size())
    @assertFalse(this%dictionary%has('alpha'), 'There should not be an "alpha".')
    @assertTrue(this%dictionary%has('beta'), 'There should be a "beta".')
    @assertTrue(this%dictionary%has('gamma'), 'There should be a "gamma".')
    @assertTrue(this%dictionary%has('delta'), 'There should be a "delta".')

    betaValue = this%dictionary%getReference('beta')
    @assertEqual(2, betaValue, 'Wrong value for "beta".')

    gammaValue = this%dictionary%getReference('gamma')
    @assertEqual(3.d+0, gammaValue, 'Wrong value for "gamma".')

    gammaValue = this%dictionary%getReference('Gamma')
    @assertEqual(3.d+0, gammaValue, 'Wrong value for "Gamma".')

    deltaValues = this%dictionary%getReference('delta')
    @assertTrue(all([.true.,.false.,.true.,.true.] .eqv. deltaValues), 'Wrong value for "delta".')

  end subroutine testComplexCase
  
end module Test_AttributeDictionary_mod
